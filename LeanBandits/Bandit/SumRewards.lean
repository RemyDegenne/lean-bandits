/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import LeanBandits.Bandit.Bandit
import LeanBandits.Bandit.Regret
import LeanBandits.ForMathlib.SubGaussian

/-! # Law of the sum of rewards
-/

open MeasureTheory ProbabilityTheory Finset Learning
open scoped ENNReal NNReal

namespace Bandits

namespace ArrayModel

lemma sum_Icc_one_eq_sum_range {m : â„•} {f : â„• â†’ â„} :
    âˆ‘ i âˆˆ Icc 1 m, f (i - 1) = âˆ‘ i âˆˆ range m, f i := by
  have h : Icc 1 m = (range m).image (Â· + 1) := by
    ext x; simp only [mem_Icc, mem_image, mem_range]; constructor
    Â· intro âŸ¨h1, h2âŸ©; exact âŸ¨x - 1, by omega, by omegaâŸ©
    Â· rintro âŸ¨a, ha, rflâŸ©; omega
  rw [h, Finset.sum_image (fun _ _ _ _ h => by omega)]
  simp

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} [DecidableEq Î±] [Countable Î±]
  [StandardBorelSpace Î±] [Nonempty Î±]
  {alg : Algorithm Î± â„} {Î½ : Kernel Î± â„} [IsMarkovKernel Î½]

local notation "A" => action alg
local notation "R" => reward alg
local notation "ğ”“" => arrayMeasure Î½

lemma identDistrib_pullCount_prod_sum_Icc_rewardByCount' (n : â„•) :
    IdentDistrib (fun Ï‰ a â†¦ (pullCount A a n Ï‰.1,
        âˆ‘ i âˆˆ Icc 1 (pullCount A a n Ï‰.1), rewardByCount A R a i Ï‰))
      (fun Ï‰ a â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ Icc 1 (pullCount A a n Ï‰), Ï‰.2 (i - 1) a))
      ((ğ”“).prod (streamMeasure Î½)) ğ”“ where
  aemeasurable_fst := by
    refine Measurable.aemeasurable ?_
    rw [measurable_pi_iff]
    refine fun a â†¦ Measurable.prod (by fun_prop) ?_
    exact measurable_sum_Icc_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)
  aemeasurable_snd := by
    refine Measurable.aemeasurable ?_
    rw [measurable_pi_iff]
    refine fun a â†¦ Measurable.prod (by fun_prop) ?_
    exact measurable_sum_Icc_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)
  map_eq := by
    by_cases hn : n = 0
    Â· simp [hn]
    have h_eq (a : Î±) (i : â„•) (Ï‰ : probSpace Î± â„ Ã— (â„• â†’ Î± â†’ â„))
        (hi : i âˆˆ Icc 1 (pullCount A a n Ï‰.1)) :
        rewardByCount A R a i Ï‰ = Ï‰.1.2 (i - 1) a := by
      rw [rewardByCount_of_stepsUntil_ne_top]
      Â· simp only [reward_eq]
        have h_exists : âˆƒ s, pullCount A a (s + 1) Ï‰.1 = i :=
          exists_pullCount_eq_of_le (n := n - 1) (by grind) (by grind)
        have h_action : A (stepsUntil A a i Ï‰.1).toNat Ï‰.1 = a :=
          action_stepsUntil (Â«AÂ» := A) (by grind) h_exists
        congr!
        rw [h_action, pullCount_stepsUntil (by grind) h_exists]
      Â· have : stepsUntil A a (pullCount A a (n + 1) Ï‰.1) Ï‰.1 â‰  âŠ¤ := by
          refine ne_top_of_le_ne_top ?_ (stepsUntil_pullCount_le _ _ _)
          simp
        refine ne_top_of_le_ne_top this ?_
        refine stepsUntil_mono a Ï‰.1 (by grind) ?_
        simp only [mem_Icc] at hi
        refine hi.2.trans ?_
        exact pullCount_mono _ (by grind) _
    have h_sum_eq (a : Î±) (Ï‰ : probSpace Î± â„ Ã— (â„• â†’ Î± â†’ â„)) :
        âˆ‘ i âˆˆ Icc 1 (pullCount A a n Ï‰.1), rewardByCount A R a i Ï‰ =
        âˆ‘ i âˆˆ Icc 1 (pullCount A a n Ï‰.1), Ï‰.1.2 (i - 1) a :=
      Finset.sum_congr rfl fun i hi â†¦ h_eq a i Ï‰ hi
    simp_rw [h_sum_eq]
    conv_rhs => rw [â† Measure.fst_prod (Î¼ := ğ”“) (Î½ := streamMeasure Î½),
      Measure.fst]
    rw [AEMeasurable.map_map_of_aemeasurable _ (by fun_prop)]
    Â· rfl
    simp only [Measure.map_fst_prod, measure_univ, one_smul]
    refine Measurable.aemeasurable ?_
    rw [measurable_pi_iff]
    refine fun a â†¦ Measurable.prod (by fun_prop) ?_
    exact measurable_sum_Icc_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)

lemma identDistrib_pullCount_prod_sum_Icc_rewardByCount (n : â„•) :
    IdentDistrib (fun Ï‰ a â†¦ (pullCount A a n Ï‰.1,
        âˆ‘ i âˆˆ Icc 1 (pullCount A a n Ï‰.1), rewardByCount A R a i Ï‰))
      (fun Ï‰ a â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a))
      ((ğ”“).prod (streamMeasure Î½)) ğ”“ := by
  convert identDistrib_pullCount_prod_sum_Icc_rewardByCount' n using 2 with Ï‰
  rotate_left
  Â· infer_instance
  Â· infer_instance
  ext a : 1
  congr 1
  exact sum_Icc_one_eq_sum_range.symm

lemma identDistrib_pullCount_prod_sumRewards (n : â„•) :
    IdentDistrib (fun Ï‰ a â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰))
      (fun Ï‰ a â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a)) ğ”“ ğ”“ := by
  suffices IdentDistrib (fun Ï‰ a â†¦ (pullCount A a n Ï‰.1, sumRewards A R a n Ï‰.1))
      (fun Ï‰ a â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a))
      ((ğ”“).prod (streamMeasure Î½)) ğ”“ by
    -- todo: missing lemma about IdentDistrib?
    constructor
    Â· refine Measurable.aemeasurable ?_
      fun_prop
    Â· refine Measurable.aemeasurable ?_
      rw [measurable_pi_iff]
      refine fun a â†¦ Measurable.prod (by fun_prop) ?_
      exact measurable_sum_range_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)
    have h_eq := this.map_eq
    nth_rw 1 [â† Measure.fst_prod (Î¼ := ğ”“) (Î½ := streamMeasure Î½), Measure.fst,
      Measure.map_map (by fun_prop) (by fun_prop)]
    exact h_eq
  simp_rw [â† sum_rewardByCount_eq_sumRewards]
  exact identDistrib_pullCount_prod_sum_Icc_rewardByCount n

lemma identDistrib_pullCount_prod_sumRewards_arm (a : Î±) (n : â„•) :
    IdentDistrib (fun Ï‰ â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰))
      (fun Ï‰ â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a)) ğ”“ ğ”“ := by
  have h1 : (fun Ï‰ â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰)) =
    (fun p â†¦ p a) âˆ˜ (fun Ï‰ a â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰)) := rfl
  have h2 : (fun Ï‰ â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a)) =
      (fun p â†¦ p a) âˆ˜
        (fun Ï‰ a â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a)) := rfl
  rw [h1, h2]
  refine (identDistrib_pullCount_prod_sumRewards n).comp ?_
  fun_prop

lemma identDistrib_pullCount_prod_sumRewards_two_arms (a b : Î±) (n : â„•) :
    IdentDistrib (fun Ï‰ â†¦ (pullCount A a n Ï‰, pullCount A b n Ï‰,
        sumRewards A R a n Ï‰, sumRewards A R b n Ï‰))
      (fun Ï‰ â†¦ (pullCount A a n Ï‰, pullCount A b n Ï‰,
        âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a,
        âˆ‘ i âˆˆ range (pullCount A b n Ï‰), Ï‰.2 i b)) ğ”“ ğ”“ := by
  have h_ident := identDistrib_pullCount_prod_sumRewards (Î½ := Î½) (alg := alg) n
  exact h_ident.comp (u := fun p â†¦ ((p a).1, (p b).1, (p a).2, (p b).2)) (by fun_prop)

lemma identDistrib_sumRewards (n : â„•) :
    IdentDistrib (fun Ï‰ a â†¦ sumRewards A R a n Ï‰)
      (fun Ï‰ a â†¦ âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a) ğ”“ ğ”“ := by
  have h_ident := identDistrib_pullCount_prod_sumRewards (Î½ := Î½) (alg := alg) n
  exact h_ident.comp (u := fun p a â†¦ (p a).2) (by fun_prop)

lemma identDistrib_sumRewards_arm (a : Î±) (n : â„•) :
    IdentDistrib (sumRewards A R a n)
      (fun Ï‰ â†¦ âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a) ğ”“ ğ”“ := by
  have h1 : sumRewards A R a n = (fun p â†¦ p a) âˆ˜ (fun Ï‰ a â†¦ sumRewards A R a n Ï‰) := rfl
  have h2 : (fun Ï‰ â†¦ âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a) =
      (fun p â†¦ p a) âˆ˜ (fun Ï‰ a â†¦ âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a) := rfl
  rw [h1, h2]
  refine (identDistrib_sumRewards n).comp ?_
  fun_prop

omit [DecidableEq Î±] [StandardBorelSpace Î±] [Nonempty Î±] in
lemma identDistrib_sum_range_snd (a : Î±) (k : â„•) :
    IdentDistrib (fun Ï‰ â†¦ âˆ‘ i âˆˆ range k, Ï‰.2 i a) (fun Ï‰ â†¦ âˆ‘ i âˆˆ range k, Ï‰ i a)
      ğ”“ (streamMeasure Î½) where
  aemeasurable_fst := by fun_prop
  aemeasurable_snd := (measurable_sum _ fun i _ â†¦ by fun_prop).aemeasurable
  map_eq := by
    rw [â† Measure.snd_prod (Î¼ := (Measure.infinitePi fun (_ : â„•) â†¦ (volume : Measure unitInterval)))
      (Î½ := streamMeasure Î½), Measure.snd, Measure.map_map (by fun_prop) (by fun_prop)]
    rfl

omit [Countable Î±] in
lemma sumRewards_eq_sum_stream_of_pullCount_eq (a : Î±) (s m : â„•) (Ï‰ : probSpace Î± â„)
    (hpc : pullCount A a s Ï‰ = m) :
    sumRewards A R a s Ï‰ = âˆ‘ i âˆˆ range m, Ï‰.2 i a := by
  let Ï‰' : probSpace Î± â„ Ã— (â„• â†’ Î± â†’ â„) := (Ï‰, Ï‰.2)
  have h_sum_rbc : sumRewards A R a s Ï‰ = âˆ‘ i âˆˆ Icc 1 m, rewardByCount A R a i Ï‰' := by
    rw [â† sum_rewardByCount_eq_sumRewards a s Ï‰', hpc]
  rw [h_sum_rbc]
  have h_rbc_eq (i : â„•) (hi : i âˆˆ Icc 1 m) : rewardByCount A R a i Ï‰' = Ï‰.2 (i - 1) a := by
    have hi' := mem_Icc.mp hi
    have hi_ne : i â‰  0 := Nat.one_le_iff_ne_zero.mp hi'.1
    have h_i_le : i â‰¤ pullCount A a s Ï‰ := hpc â–¸ hi'.2
    have hs_pos : 0 < s :=
      Nat.pos_of_ne_zero (by rintro rfl; simp [pullCount] at hpc; omega)
    have h_exists : âˆƒ t, pullCount A a (t + 1) Ï‰ = i :=
      exists_pullCount_eq_of_le (n := s - 1) (Nat.sub_add_cancel hs_pos â–¸ h_i_le) hi_ne
    rw [rewardByCount_of_stepsUntil_ne_top (stepsUntil_ne_top h_exists)]
    simp only [reward_eq]
    have h_action : A (stepsUntil A a i Ï‰).toNat Ï‰ = a :=
      action_stepsUntil (Â«AÂ» := A) hi_ne h_exists
    congr!
    rw [h_action, pullCount_stepsUntil hi_ne h_exists]
  calc âˆ‘ i âˆˆ Icc 1 m, rewardByCount A R a i Ï‰'
    _ = âˆ‘ i âˆˆ Icc 1 m, Ï‰.2 (i - 1) a := Finset.sum_congr rfl h_rbc_eq
    _ = âˆ‘ j âˆˆ range m, Ï‰.2 j a := sum_Icc_one_eq_sum_range (f := fun i => Ï‰.2 i a)

lemma prob_pullCount_prod_sumRewards_mem_le (a : Î±) (n : â„•)
    {s : Set (â„• Ã— â„)} [DecidablePred (Â· âˆˆ Prod.fst '' s)] (hs : MeasurableSet s) :
    ğ”“ {Ï‰ | (pullCount A a n Ï‰, sumRewards A R a n Ï‰) âˆˆ s} â‰¤
      âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ Prod.fst '' s),
        streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ Prod.mk k â»Â¹' s} := by
  have h_ident := identDistrib_pullCount_prod_sumRewards_arm a n (Î½ := Î½) (alg := alg)
  have : ğ”“ {Ï‰ | (pullCount A a n Ï‰, sumRewards A R a n Ï‰) âˆˆ s} =
      (ğ”“).map (fun Ï‰ â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰)) s := by
    rw [Measure.map_apply (by fun_prop) hs]
    rfl
  rw [this, h_ident.map_eq, Measure.map_apply ?_ hs]
  swap
  Â· refine Measurable.prod (by fun_prop) ?_
    exact measurable_sum_range_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)
  calc ğ”“ ((fun Ï‰ â†¦ (pullCount A a n Ï‰, âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a)) â»Â¹' s)
  _ â‰¤ ğ”“ {Ï‰ | âˆƒ k â‰¤ n, (k, âˆ‘ i âˆˆ range k, Ï‰.2 i a) âˆˆ s} := by
    refine measure_mono fun Ï‰ hÏ‰ â†¦ ?_
    simp only [Set.mem_setOf_eq] at hÏ‰ âŠ¢
    exact âŸ¨pullCount A a n Ï‰, pullCount_le _ _ _, hÏ‰âŸ©
  _ = ğ”“ (â‹ƒ k âˆˆ (range (n + 1)).filter (Â· âˆˆ Prod.fst '' s),
      {Ï‰ | (k, âˆ‘ i âˆˆ range k, Ï‰.2 i a) âˆˆ s}) := by congr 1; ext; simp; grind
  _ â‰¤ âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ Prod.fst '' s),
      ğ”“ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰.2 i a âˆˆ Prod.mk k â»Â¹' s} := measure_biUnion_finset_le _ _
  _ = âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ Prod.fst '' s),
      (streamMeasure Î½) {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ Prod.mk k â»Â¹' s} := by
    congr with k
    have : (ğ”“).map (fun Ï‰ â†¦ âˆ‘ i âˆˆ range k, Ï‰.2 i a) =
        (streamMeasure Î½).map (fun Ï‰ â†¦ âˆ‘ i âˆˆ range k, Ï‰ i a) :=
      (identDistrib_sum_range_snd a k).map_eq
    rw [Measure.ext_iff] at this
    specialize this (Prod.mk k â»Â¹' s) (hs.preimage (by fun_prop))
    rwa [Measure.map_apply (by fun_prop) (hs.preimage (by fun_prop)),
      Measure.map_apply (by fun_prop) (hs.preimage (by fun_prop))] at this

lemma prob_pullCount_mem_and_sumRewards_mem_le (a : Î±) (n : â„•)
    {s : Set â„•} [DecidablePred (Â· âˆˆ s)] (hs : MeasurableSet s) {B : Set â„} (hB : MeasurableSet B) :
    ğ”“ {Ï‰ | pullCount A a n Ï‰ âˆˆ s âˆ§ sumRewards A R a n Ï‰ âˆˆ B} â‰¤
      âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ s),
        streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ B} := by
  classical
  rcases Set.eq_empty_or_nonempty B with h_empty | h_nonempty
  Â· simp [h_empty]
  convert prob_pullCount_prod_sumRewards_mem_le a n (hs.prod hB) (Î½ := Î½) (alg := alg) with _ _ k hk
  Â· ext n
    have : âˆƒ x, x âˆˆ B := h_nonempty
    simp [this]
  Â· ext x
    simp only [Set.mem_image, Set.mem_prod, Prod.exists, exists_and_right, exists_and_left,
      exists_eq_right, mem_filter, mem_range] at hk
    simp [hk.2.1]

lemma prob_exists_pullCount_eq_and_sumRewards_mem_le (a : Î±) (n m : â„•)
    {B : Set â„} (hB : MeasurableSet B) :
    ğ”“ {Ï‰ | âˆƒ s, s â‰¤ n âˆ§ pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B} â‰¤
      streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range m, Ï‰ i a âˆˆ B} := by
  calc ğ”“ {Ï‰ | âˆƒ s, s â‰¤ n âˆ§ pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B}
    _ â‰¤ ğ”“ {Ï‰ | âˆ‘ i âˆˆ range m, Ï‰.2 i a âˆˆ B} := by
        -- Show the containment: the existential set âŠ† {sum âˆˆ B}
        apply measure_mono
        intro Ï‰ âŸ¨s, _hs, hpc, hB'âŸ©
        -- When pullCount(s, Ï‰) = m, sumRewards(s, Ï‰) = âˆ‘ i < m, Ï‰.2 i a in the ArrayModel.
        rw [sumRewards_eq_sum_stream_of_pullCount_eq a s m Ï‰ hpc] at hB'
        exact hB'
    _ = streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range m, Ï‰ i a âˆˆ B} := by
        have := (identDistrib_sum_range_snd (Î½ := Î½) a m).map_eq
        rw [Measure.ext_iff] at this
        specialize this B hB
        rwa [Measure.map_apply (by fun_prop) hB, Measure.map_apply (by fun_prop) hB] at this

lemma prob_sumRewards_le_sumRewards_le [Fintype Î±] (a : Î±) (n mâ‚ mâ‚‚ : â„•) :
    (ğ”“) {Ï‰ | pullCount A (bestArm Î½) n Ï‰ = mâ‚ âˆ§ pullCount A a n Ï‰ = mâ‚‚ âˆ§
        sumRewards A R (bestArm Î½) n Ï‰ â‰¤ sumRewards A R a n Ï‰} â‰¤
      streamMeasure Î½
        {Ï‰ | âˆ‘ i âˆˆ range mâ‚, Ï‰ i (bestArm Î½) â‰¤ âˆ‘ i âˆˆ range mâ‚‚, Ï‰ i a} := by
  have h_ident := identDistrib_pullCount_prod_sumRewards_two_arms (bestArm Î½) a n
    (Î½ := Î½) (alg := alg)
  let s := {p : â„• Ã— â„• Ã— â„ Ã— â„ | p.1 = mâ‚ âˆ§ p.2.1 = mâ‚‚ âˆ§ p.2.2.1 â‰¤ p.2.2.2}
  have hs : MeasurableSet s := by simp only [measurableSet_setOf, s]; fun_prop
  calc ğ”“ {Ï‰ | pullCount A (bestArm Î½) n Ï‰ = mâ‚ âˆ§ pullCount A a n Ï‰ = mâ‚‚ âˆ§
      sumRewards A R (bestArm Î½) n Ï‰ â‰¤ sumRewards A R a n Ï‰}
  _ = ğ”“ ((fun Ï‰ â†¦ (pullCount A (bestArm Î½) n Ï‰, pullCount A a n Ï‰,
        sumRewards A R (bestArm Î½) n Ï‰, sumRewards A R a n Ï‰)) â»Â¹'
        {p | p.1 = mâ‚ âˆ§ p.2.1 = mâ‚‚ âˆ§ p.2.2.1 â‰¤ p.2.2.2}) := rfl
  _ = ğ”“ ((fun Ï‰ â†¦ (pullCount A (bestArm Î½) n Ï‰, pullCount A a n Ï‰,
        âˆ‘ i âˆˆ range (pullCount A (bestArm Î½) n Ï‰), Ï‰.2 i (bestArm Î½),
        âˆ‘ i âˆˆ range (pullCount A a n Ï‰), Ï‰.2 i a)) â»Â¹'
        {p | p.1 = mâ‚ âˆ§ p.2.1 = mâ‚‚ âˆ§ p.2.2.1 â‰¤ p.2.2.2}) := by
      rw [â† Measure.map_apply (by fun_prop) hs, h_ident.map_eq,
        Measure.map_apply _ hs]
      refine Measurable.prod (by fun_prop) (Measurable.prod (by fun_prop) ?_)
      refine Measurable.prod ?_ ?_
      Â· exact measurable_sum_range_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)
      Â· exact measurable_sum_range_of_le (n := n) (pullCount_le _ _) (by fun_prop) (by fun_prop)
  _ â‰¤ ğ”“ ((fun Ï‰ â†¦ (âˆ‘ i âˆˆ range mâ‚, Ï‰.2 i (bestArm Î½), âˆ‘ i âˆˆ range mâ‚‚, Ï‰.2 i a)) â»Â¹'
        {p | p.1 â‰¤ p.2}) := by
      refine measure_mono fun Ï‰ hÏ‰ â†¦ ?_
      simp only [Set.preimage_setOf_eq, Set.mem_setOf_eq] at hÏ‰ âŠ¢
      grind
  _ = streamMeasure Î½
      {Ï‰ | âˆ‘ i âˆˆ range mâ‚, Ï‰ i (bestArm Î½) â‰¤ âˆ‘ i âˆˆ range mâ‚‚, Ï‰ i a} := by
    rw [â† Measure.snd_prod (Î¼ := (Measure.infinitePi fun (_ : â„•) â†¦ (volume : Measure unitInterval)))
      (Î½ := streamMeasure Î½), Measure.snd, Measure.map_apply (by fun_prop)]
    Â· rfl
    simp only [measurableSet_setOf]
    fun_prop

lemma probReal_sumRewards_le_sumRewards_le [Fintype Î±] (a : Î±) (n mâ‚ mâ‚‚ : â„•) :
    (ğ”“).real {Ï‰ | pullCount A (bestArm Î½) n Ï‰ = mâ‚ âˆ§ pullCount A a n Ï‰ = mâ‚‚ âˆ§
        sumRewards A R (bestArm Î½) n Ï‰ â‰¤ sumRewards A R a n Ï‰} â‰¤
      (streamMeasure Î½).real
        {Ï‰ | âˆ‘ i âˆˆ range mâ‚, Ï‰ i (bestArm Î½) â‰¤ âˆ‘ i âˆˆ range mâ‚‚, Ï‰ i a} := by
  simp_rw [measureReal_def]
  gcongr
  Â· finiteness
  Â· exact prob_sumRewards_le_sumRewards_le a n mâ‚ mâ‚‚

end ArrayModel

variable {Î± Î© Î©' : Type*} [DecidableEq Î±] {mÎ± : MeasurableSpace Î±} {mÎ© : MeasurableSpace Î©}
  {mÎ©' : MeasurableSpace Î©'}
  {P : Measure Î©} [IsProbabilityMeasure P] {P' : Measure Î©'} [IsProbabilityMeasure P']
  {alg : Algorithm Î± â„} {Î½ : Kernel Î± â„} [IsMarkovKernel Î½]
  {A : â„• â†’ Î© â†’ Î±} {R : â„• â†’ Î© â†’ â„} {Aâ‚‚ : â„• â†’ Î©' â†’ Î±} {Râ‚‚ : â„• â†’ Î©' â†’ â„}
  {Ï‰ : Î©} {m n t : â„•} {a : Î±}

lemma sumRewards_eq_comp :
    sumRewards A R a n =
     (fun p â†¦ âˆ‘ i âˆˆ range n, if (p i).1 = a then (p i).2 else 0) âˆ˜ (fun Ï‰ n â†¦ (A n Ï‰, R n Ï‰)) := by
  ext
  simp [sumRewards]

lemma pullCount_eq_comp :
    pullCount A a n =
      (fun p â†¦ âˆ‘ i âˆˆ range n, if (p i).1 = a then 1 else 0) âˆ˜ (fun Ï‰ n â†¦ (A n Ï‰, R n Ï‰)) := by
  ext
  simp [pullCount]

variable [StandardBorelSpace Î±] [Nonempty Î±]

-- todo: write those lemmas with IdentDistrib instead of equality of maps
lemma _root_.Learning.IsAlgEnvSeq.law_sumRewards_unique
    (h1 : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    (h2 : IsAlgEnvSeq Aâ‚‚ Râ‚‚ alg (stationaryEnv Î½) P') :
    P.map (sumRewards A R a n) = P'.map (sumRewards Aâ‚‚ Râ‚‚ a n) := by
  have hA := h1.measurable_A
  have hR := h1.measurable_R
  have hA2 := h2.measurable_A
  have hR2 := h2.measurable_R
  have h_unique := isAlgEnvSeq_unique h1 h2
  rw [sumRewards_eq_comp, sumRewards_eq_comp, â† Measure.map_map, h_unique, Measure.map_map,
    â† sumRewards_eq_comp]
  Â· refine measurable_sum _ fun i hi â†¦ Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  Â· rw [measurable_pi_iff]
    exact fun n â†¦ Measurable.prodMk (hA2 n) (hR2 n)
  Â· refine measurable_sum _ fun i hi â†¦ Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  Â· rw [measurable_pi_iff]
    exact fun n â†¦ Measurable.prodMk (hA n) (hR n)

lemma _root_.Learning.IsAlgEnvSeq.law_pullCount_sumRewards_unique'
    (h1 : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    (h2 : IsAlgEnvSeq Aâ‚‚ Râ‚‚ alg (stationaryEnv Î½) P') :
    IdentDistrib (fun Ï‰ a â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰))
      (fun Ï‰ a â†¦ (pullCount Aâ‚‚ a n Ï‰, sumRewards Aâ‚‚ Râ‚‚ a n Ï‰)) P P' := by
  have hA := h1.measurable_A
  have hR := h1.measurable_R
  have hA2 := h2.measurable_A
  have hR2 := h2.measurable_R
  constructor
  Â· refine Measurable.aemeasurable ?_
    rw [measurable_pi_iff]
    exact fun a â†¦ Measurable.prod (by fun_prop) (measurable_sumRewards hA hR _ _)
  Â· refine Measurable.aemeasurable ?_
    rw [measurable_pi_iff]
    exact fun a â†¦ Measurable.prod (by fun_prop) (measurable_sumRewards hA2 hR2 _ _)
  have h_unique := isAlgEnvSeq_unique h1 h2
  let f := fun (p : â„• â†’ Î± Ã— â„ ) (a : Î±) â†¦ (âˆ‘ i âˆˆ range n, if (p i).1 = a then 1 else 0,
    âˆ‘ i âˆˆ range n, if (p i).1 = a then (p i).2 else 0)
  have hf : Measurable f := by
    rw [measurable_pi_iff]
    intro a
    refine Measurable.prod ?_ ?_
    Â· simp only [f]
      refine measurable_sum _ fun i hi â†¦ Measurable.ite ?_ (by fun_prop) (by fun_prop)
      exact (measurableSet_singleton _).preimage (by fun_prop)
    Â· simp only [f]
      refine measurable_sum _ fun i hi â†¦ Measurable.ite ?_ (by fun_prop) (by fun_prop)
      exact (measurableSet_singleton _).preimage (by fun_prop)
  have h_eq_comp : (fun Ï‰ a â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰))
      = f âˆ˜ (fun Ï‰ n â†¦ (A n Ï‰, R n Ï‰)) := by
    ext Ï‰ a : 2
    rw [pullCount_eq_comp (R := R), sumRewards_eq_comp]
    grind
  have h_eq_comp2 : (fun Ï‰ a â†¦ (pullCount Aâ‚‚ a n Ï‰, sumRewards Aâ‚‚ Râ‚‚ a n Ï‰))
      = f âˆ˜ (fun Ï‰ n â†¦ (Aâ‚‚ n Ï‰, Râ‚‚ n Ï‰)) := by
    ext Ï‰ a : 2
    rw [pullCount_eq_comp (R := Râ‚‚), sumRewards_eq_comp]
    grind
  rw [h_eq_comp, h_eq_comp2, â† Measure.map_map hf, h_unique, Measure.map_map hf,
    â† h_eq_comp2]
  Â· rw [measurable_pi_iff]
    exact fun n â†¦ Measurable.prodMk (hA2 n) (hR2 n)
  Â· rw [measurable_pi_iff]
    exact fun n â†¦ Measurable.prodMk (hA n) (hR n)

lemma _root_.Learning.IsAlgEnvSeq.law_pullCount_sumRewards_unique
    (h1 : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    (h2 : IsAlgEnvSeq Aâ‚‚ Râ‚‚ alg (stationaryEnv Î½) P') :
    P.map (fun Ï‰ â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰)) =
      P'.map (fun Ï‰ â†¦ (pullCount Aâ‚‚ a n Ï‰, sumRewards Aâ‚‚ Râ‚‚ a n Ï‰)) :=
  ((h1.law_pullCount_sumRewards_unique' h2 (n := n)).comp
    (u := fun f â†¦ f a) (by fun_prop)).map_eq

-- this is what we will use for UCB
lemma prob_pullCount_prod_sumRewards_mem_le [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    {s : Set (â„• Ã— â„)} [DecidablePred (Â· âˆˆ Prod.fst '' s)] (hs : MeasurableSet s) :
    P {Ï‰ | (pullCount A a n Ï‰, sumRewards A R a n Ï‰) âˆˆ s} â‰¤
      âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ Prod.fst '' s),
        streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ Prod.mk k â»Â¹' s} := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  calc P {Ï‰ | (pullCount A a n Ï‰, sumRewards A R a n Ï‰) âˆˆ s}
  _ = (P.map (fun Ï‰ â†¦ (pullCount A a n Ï‰, sumRewards A R a n Ï‰))) s := by
      rw [Measure.map_apply (by fun_prop) hs]; rfl
  _ = ((ArrayModel.arrayMeasure Î½).map
      (fun Ï‰ â†¦ (pullCount (ArrayModel.action alg) a n Ï‰,
        sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a n Ï‰))) s := by
    rw [h.law_pullCount_sumRewards_unique (ArrayModel.isAlgEnvSeq_arrayMeasure alg Î½)]
  _ = (ArrayModel.arrayMeasure Î½) {Ï‰ | (pullCount (ArrayModel.action alg) a n Ï‰,
      sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a n Ï‰) âˆˆ s} := by
    rw [Measure.map_apply (by fun_prop) hs]; rfl
  _ â‰¤ âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ Prod.fst '' s),
      streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ Prod.mk k â»Â¹' s} :=
    ArrayModel.prob_pullCount_prod_sumRewards_mem_le a n hs

lemma prob_pullCount_mem_and_sumRewards_mem_le [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    {s : Set â„•} [DecidablePred (Â· âˆˆ s)] (hs : MeasurableSet s) {B : Set â„} (hB : MeasurableSet B) :
    P {Ï‰ | pullCount A a n Ï‰ âˆˆ s âˆ§ sumRewards A R a n Ï‰ âˆˆ B} â‰¤
      âˆ‘ k âˆˆ (range (n + 1)).filter (Â· âˆˆ s),
        streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ B} := by
  classical
  rcases Set.eq_empty_or_nonempty B with h_empty | h_nonempty
  Â· simp [h_empty]
  convert prob_pullCount_prod_sumRewards_mem_le h (hs.prod hB) (Î½ := Î½) (alg := alg) with _ _ k hk
  Â· ext n
    have : âˆƒ x, x âˆˆ B := h_nonempty
    simp [this]
  Â· ext x
    simp only [Set.mem_image, Set.mem_prod, Prod.exists, exists_and_right, exists_and_left,
      exists_eq_right, mem_filter, mem_range] at hk
    simp [hk.2.1]

lemma prob_sumRewards_mem_le [Countable Î±] (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    {B : Set â„} (hB : MeasurableSet B) :
    P (sumRewards A R a n â»Â¹' B) â‰¤
      âˆ‘ k âˆˆ range (n + 1), streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range k, Ï‰ i a âˆˆ B} := by
  classical
  have h_le := prob_pullCount_mem_and_sumRewards_mem_le h .univ hB (a := a) (n := n)
  simpa using h_le

lemma prob_pullCount_eq_and_sumRewards_mem_le [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    {m : â„•} (hm : m â‰¤ n) {B : Set â„} (hB : MeasurableSet B) :
    P {Ï‰ | pullCount A a n Ï‰ = m âˆ§ sumRewards A R a n Ï‰ âˆˆ B} â‰¤
      streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range m, Ï‰ i a âˆˆ B} := by
  have h_le := prob_pullCount_mem_and_sumRewards_mem_le h (s := {m}) (by simp) hB (a := a) (n := n)
  have hm' : m < n + 1 := by lia
  simpa [hm'] using h_le

lemma prob_exists_pullCount_eq_and_sumRewards_mem_le [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    {m : â„•} {B : Set â„} (hB : MeasurableSet B) :
    P {Ï‰ | âˆƒ s, s â‰¤ n âˆ§ pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B} â‰¤
      streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range m, Ï‰ i a âˆˆ B} := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  have h_eq : {Ï‰ | âˆƒ s, s â‰¤ n âˆ§ pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B} =
      â‹ƒ s âˆˆ range (n + 1), {Ï‰ | pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B} := by
    ext Ï‰; simp only [Set.mem_setOf_eq, Set.mem_iUnion, mem_range]
    constructor <;> rintro âŸ¨s, hs, restâŸ© <;> exact âŸ¨s, by omega, restâŸ©
  rw [h_eq]
  have h_AM := ArrayModel.prob_exists_pullCount_eq_and_sumRewards_mem_le
    (Î½ := Î½) (alg := alg) a n m hB
  let pc := fun (p : â„• â†’ Î± Ã— â„) (s : â„•) â†¦ âˆ‘ i âˆˆ range s, if (p i).1 = a then 1 else 0
  let sr := fun (p : â„• â†’ Î± Ã— â„) (s : â„•) â†¦ âˆ‘ i âˆˆ range s, if (p i).1 = a then (p i).2 else 0
  let S := â‹ƒ s âˆˆ range (n + 1), {p : â„• â†’ Î± Ã— â„ | pc p s = m âˆ§ sr p s âˆˆ B}
  have hS : MeasurableSet S := by
    simp only [S]
    apply MeasurableSet.iUnion
    intro s
    apply MeasurableSet.iUnion
    intro _
    apply MeasurableSet.inter
    Â· exact (measurableSet_singleton _).preimage
        (measurable_sum _ fun i _ â†¦ Measurable.ite
          ((measurableSet_singleton _).preimage (by fun_prop)) (by fun_prop) (by fun_prop))
    Â· exact hB.preimage
        (measurable_sum _ fun i _ â†¦ Measurable.ite
          ((measurableSet_singleton _).preimage (by fun_prop)) (by fun_prop) (by fun_prop))
  have h_eq1 : (â‹ƒ s âˆˆ range (n + 1), {Ï‰ | pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B}) =
      (fun Ï‰ t â†¦ (A t Ï‰, R t Ï‰)) â»Â¹' S := by
    ext Ï‰
    simp only [Set.mem_iUnion, mem_range, Set.mem_setOf_eq, Set.mem_preimage, S, pc, sr,
      pullCount, sumRewards, Finset.card_filter]
  have h_eq2 : (â‹ƒ s âˆˆ range (n + 1), {Ï‰ | pullCount (ArrayModel.action alg) a s Ï‰ = m âˆ§
      sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a s Ï‰ âˆˆ B}) =
      (fun Ï‰ t â†¦ (ArrayModel.action alg t Ï‰, ArrayModel.reward alg t Ï‰)) â»Â¹' S := by
    ext Ï‰
    simp only [Set.mem_iUnion, mem_range, Set.mem_setOf_eq, Set.mem_preimage, S, pc, sr,
      pullCount, sumRewards, Finset.card_filter]
  have h_unique := isAlgEnvSeq_unique h (ArrayModel.isAlgEnvSeq_arrayMeasure alg Î½)
  calc P (â‹ƒ s âˆˆ range (n + 1), {Ï‰ | pullCount A a s Ï‰ = m âˆ§ sumRewards A R a s Ï‰ âˆˆ B})
    _ = (ArrayModel.arrayMeasure Î½)
          (â‹ƒ s âˆˆ range (n + 1), {Ï‰ | pullCount (ArrayModel.action alg) a s Ï‰ = m âˆ§
            sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a s Ï‰ âˆˆ B}) := by
        rw [h_eq1, h_eq2, â† Measure.map_apply _ hS, â† Measure.map_apply _ hS, h_unique]
        Â· rw [measurable_pi_iff]; intro t; exact (by fun_prop : Measurable fun Ï‰ â†¦
            (ArrayModel.action alg t Ï‰, ArrayModel.reward alg t Ï‰))
        Â· rw [measurable_pi_iff]; intro t; exact (hA t).prodMk (hR t)
    _ â‰¤ streamMeasure Î½ {Ï‰ | âˆ‘ i âˆˆ range m, Ï‰ i a âˆˆ B} := by
        have h_set_eq : (â‹ƒ s âˆˆ range (n + 1), {Ï‰ | pullCount (ArrayModel.action alg) a s Ï‰ = m âˆ§
            sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a s Ï‰ âˆˆ B}) =
            {Ï‰ | âˆƒ s, s â‰¤ n âˆ§ pullCount (ArrayModel.action alg) a s Ï‰ = m âˆ§
              sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a s Ï‰ âˆˆ B} := by
          ext Ï‰; simp only [Set.mem_iUnion, mem_range, Set.mem_setOf_eq]
          constructor <;> rintro âŸ¨s, hs, restâŸ© <;> exact âŸ¨s, by omega, restâŸ©
        rw [h_set_eq]
        exact h_AM

lemma probReal_sumRewards_le_sumRewards_le [Fintype Î±] (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    (a : Î±) (n mâ‚ mâ‚‚ : â„•) :
    P.real {Ï‰ | pullCount A (bestArm Î½) n Ï‰ = mâ‚ âˆ§ pullCount A a n Ï‰ = mâ‚‚ âˆ§
        sumRewards A R (bestArm Î½) n Ï‰ â‰¤ sumRewards A R a n Ï‰} â‰¤
      (streamMeasure Î½).real
        {Ï‰ | âˆ‘ i âˆˆ range mâ‚, Ï‰ i (bestArm Î½) â‰¤ âˆ‘ i âˆˆ range mâ‚‚, Ï‰ i a} := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  refine le_trans (le_of_eq ?_)
    (ArrayModel.probReal_sumRewards_le_sumRewards_le (alg := alg) a n mâ‚ mâ‚‚)
  let s := {p : â„• Ã— â„• Ã— â„ Ã— â„ | p.1 = mâ‚ âˆ§ p.2.1 = mâ‚‚ âˆ§ p.2.2.1 â‰¤ p.2.2.2}
  have hs : MeasurableSet s := by simp only [measurableSet_setOf, s]; fun_prop
  change P.real ((fun Ï‰ â†¦ (pullCount A (bestArm Î½) n Ï‰,
      pullCount A a n Ï‰, sumRewards A R (bestArm Î½) n Ï‰, sumRewards A R a n Ï‰)) â»Â¹' s) =
    (ArrayModel.arrayMeasure Î½).real
      ((fun Ï‰ â†¦ (pullCount (ArrayModel.action alg) (bestArm Î½) n Ï‰,
        pullCount (ArrayModel.action alg) a n Ï‰,
        sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) (bestArm Î½) n Ï‰,
        sumRewards (ArrayModel.action alg) (ArrayModel.reward alg) a n Ï‰)) â»Â¹' s)
  simp_rw [measureReal_def]
  congr 1
  rw [â† Measure.map_apply ?_ hs, â† Measure.map_apply (by fun_prop) hs]
  swap
  Â· refine Measurable.prod (by fun_prop) (Measurable.prod (by fun_prop) ?_)
    exact (measurable_sumRewards hA hR _ _).prod (measurable_sumRewards hA hR _ _)
  congr 1
  refine IdentDistrib.map_eq ?_
  have h_eq := h.law_pullCount_sumRewards_unique' (ArrayModel.isAlgEnvSeq_arrayMeasure alg Î½)
    (n := n)
  exact h_eq.comp (u := fun p â†¦ ((p (bestArm Î½)).1, (p a).1, (p (bestArm Î½)).2, (p a).2))
    (by fun_prop)

section Subgaussian

omit [DecidableEq Î±] [StandardBorelSpace Î±] in
lemma probReal_sum_le_sum_streamMeasure [Fintype Î±] {c : â„â‰¥0}
    (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) c (Î½ a)) (a : Î±) (m : â„•) :
    (streamMeasure Î½).real
        {Ï‰ | âˆ‘ s âˆˆ range m, Ï‰ s (bestArm Î½) â‰¤ âˆ‘ s âˆˆ range m, Ï‰ s a} â‰¤
      Real.exp (-â†‘m * gap Î½ a ^ 2 / (4 * c)) := by
  by_cases ha : a = bestArm Î½
  Â· simp [ha]
  refine (HasSubgaussianMGF.measure_sum_le_sum_le' (cX := fun _ â†¦ c) (cY := fun _ â†¦ c)
    ?_ ?_ ?_ ?_ ?_ ?_).trans_eq ?_
  Â· exact iIndepFun_eval_streamMeasure'' Î½ (bestArm Î½)
  Â· exact iIndepFun_eval_streamMeasure'' Î½ a
  Â· intro i him
    simp_rw [integral_eval_streamMeasure]
    refine (hÎ½ (bestArm Î½)).congr_identDistrib ?_
    exact (identDistrib_eval_eval_id_streamMeasure _ _ _).symm.sub_const _
  Â· intro i him
    simp_rw [integral_eval_streamMeasure]
    refine (hÎ½ a).congr_identDistrib ?_
    exact (identDistrib_eval_eval_id_streamMeasure _ _ _).symm.sub_const _
  Â· exact indepFun_eval_streamMeasure' Î½ (Ne.symm ha)
  Â· gcongr 1 with i him
    simp_rw [integral_eval_streamMeasure]
    exact le_bestArm a
  Â· congr 1
    simp_rw [integral_eval_streamMeasure]
    simp only [id_eq, sum_const, card_range, nsmul_eq_mul, NNReal.coe_mul, NNReal.coe_natCast,
      gap_eq_bestArm_sub, neg_mul]
    field_simp
    ring

omit [DecidableEq Î±] [StandardBorelSpace Î±] [Nonempty Î±] in
lemma prob_sum_le_sqrt_log {Ïƒ2 : â„â‰¥0}
    (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) Ïƒ2 (Î½ a))
    (hÏƒ2 : Ïƒ2 â‰  0) {c : â„} (hc : 0 â‰¤ c) (a : Î±) (k : â„•) (hk : k â‰  0) :
    streamMeasure Î½
        {Ï‰ | (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id])) â‰¤ - âˆš(2 * c * k * Ïƒ2 * Real.log (n + 1))} â‰¤
      1 / (n + 1) ^ c := by
  calc
    streamMeasure Î½
      {Ï‰ | (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id])) â‰¤ - âˆš(2 * c * k * Ïƒ2 * Real.log (n + 1))}
  _ â‰¤ ENNReal.ofReal (Real.exp (-(âˆš(2 * c * k * Ïƒ2 * Real.log (n + 1))) ^ 2 / (2 * k * Ïƒ2))) := by
    rw [â† ofReal_measureReal]
    gcongr
    refine (HasSubgaussianMGF.measure_sum_range_le_le_of_iIndepFun (c := Ïƒ2) ?_ ?_ (by positivity))
    Â· exact (iIndepFun_eval_streamMeasure'' Î½ a).comp (fun i Ï‰ â†¦ Ï‰ - (Î½ a)[id])
        (fun _ â†¦ by fun_prop)
    Â· intro i him
      refine (hÎ½ a).congr_identDistrib ?_
      exact (identDistrib_eval_eval_id_streamMeasure _ _ _).symm.sub_const _
  _ = 1 / (n + 1) ^ c := by
    rw [Real.sq_sqrt]
    swap; Â· exact mul_nonneg (by positivity) (Real.log_nonneg (by simp))
    field_simp
    rw [â† Real.log_rpow (by positivity), â† Real.log_inv,
      Real.exp_log (by positivity), one_div, ENNReal.ofReal_inv_of_pos (by positivity),
      â† ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
    norm_cast

omit [DecidableEq Î±] [StandardBorelSpace Î±] [Nonempty Î±] in
lemma prob_sum_ge_sqrt_log {Ïƒ2 : â„â‰¥0}
    (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) Ïƒ2 (Î½ a))
    (hÏƒ2 : Ïƒ2 â‰  0) {c : â„} (hc : 0 â‰¤ c) (a : Î±) (k : â„•) (hk : k â‰  0) :
    streamMeasure Î½
        {Ï‰ | âˆš(2 * c * k * Ïƒ2 * Real.log (n + 1)) â‰¤ (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id]))} â‰¤
      1 / (n + 1) ^ c := by
  calc
    streamMeasure Î½
      {Ï‰ | âˆš(2 * c * k * Ïƒ2 * Real.log (n + 1)) â‰¤ (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id]))}
  _ â‰¤ ENNReal.ofReal (Real.exp (-(âˆš(2 * c * k * Ïƒ2 * Real.log (n + 1))) ^ 2 / (2 * k * Ïƒ2))) := by
    rw [â† ofReal_measureReal]
    gcongr
    refine (HasSubgaussianMGF.measure_sum_range_ge_le_of_iIndepFun (c := Ïƒ2) ?_ ?_ (by positivity))
    Â· exact (iIndepFun_eval_streamMeasure'' Î½ a).comp (fun i Ï‰ â†¦ Ï‰ - (Î½ a)[id])
        (fun _ â†¦ by fun_prop)
    Â· intro i him
      refine (hÎ½ a).congr_identDistrib ?_
      exact (identDistrib_eval_eval_id_streamMeasure _ _ _).symm.sub_const _
  _ = 1 / (n + 1) ^ c := by
    rw [Real.sq_sqrt]
    swap; Â· exact mul_nonneg (by positivity) (Real.log_nonneg (by simp))
    field_simp
    rw [â† Real.log_rpow (by positivity), â† Real.log_inv,
      Real.exp_log (by positivity), one_div, ENNReal.ofReal_inv_of_pos (by positivity),
      â† ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
    norm_cast

open Real

omit [DecidableEq Î±] [StandardBorelSpace Î±] [Nonempty Î±] in
lemma streamMeasure_sampleMean_add_sqrt_le {Ïƒ2 : â„â‰¥0} {c : â„}
    (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) Ïƒ2 (Î½ a)) (hÏƒ2 : Ïƒ2 â‰  0)
    (hc : 0 â‰¤ c) (a : Î±) (n k : â„•) (hk : k â‰  0) :
    streamMeasure Î½ {Ï‰ | (âˆ‘ m âˆˆ range k, Ï‰ m a) / k + âˆš(2 * c * Ïƒ2 * log (n + 1) / k) â‰¤ (Î½ a)[id]} â‰¤
      1 / (n + 1) ^ c := by
  have h_log_nonneg : 0 â‰¤ log (n + 1) := log_nonneg (by simp)
  calc
    streamMeasure Î½ {Ï‰ | (âˆ‘ m âˆˆ range k, Ï‰ m a) / k + âˆš(2 * c * Ïƒ2 * log (n + 1) / k) â‰¤ (Î½ a)[id]}
  _ = streamMeasure Î½
      {Ï‰ | (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id])) / k â‰¤ - âˆš(2 * c * Ïƒ2 * log (n + 1) / k)} := by
    congr with Ï‰
    field_simp
    rw [Finset.sum_sub_distrib]
    simp
    grind
  _ = streamMeasure Î½
      {Ï‰ | (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id])) â‰¤ - âˆš(2 * c * k * Ïƒ2 * log (n + 1))} := by
    congr with Ï‰
    field_simp
    congr! 2
    rw [sqrt_div (by positivity), â† mul_div_assoc, mul_comm, mul_div_assoc, div_sqrt,
      mul_assoc (k : â„), mul_assoc (k : â„), mul_assoc (k : â„),
      sqrt_mul (x := (k : â„)) (by positivity), mul_comm]
  _ â‰¤ 1 / (n + 1) ^ c := prob_sum_le_sqrt_log hÎ½ hÏƒ2 hc a k hk

omit [DecidableEq Î±] [StandardBorelSpace Î±] [Nonempty Î±] in
lemma streamMeasure_le_sampleMean_sub_sqrt {Ïƒ2 : â„â‰¥0} {c : â„}
    (hÎ½ : âˆ€ a, HasSubgaussianMGF (fun x â†¦ x - (Î½ a)[id]) Ïƒ2 (Î½ a)) (hÏƒ2 : Ïƒ2 â‰  0)
    (hc : 0 â‰¤ c) (a : Î±) (n k : â„•) (hk : k â‰  0) :
    streamMeasure Î½
        {Ï‰ | (Î½ a)[id] â‰¤ (âˆ‘ m âˆˆ range k, Ï‰ m a) / k - âˆš(2 * c * Ïƒ2 *log (n + 1) / k)} â‰¤
      1 / (n + 1) ^ c := by
  have h_log_nonneg : 0 â‰¤ log (n + 1) := log_nonneg (by simp)
  calc
    streamMeasure Î½ {Ï‰ | (Î½ a)[id] â‰¤ (âˆ‘ m âˆˆ range k, Ï‰ m a) / k - âˆš(2 * c * Ïƒ2 * log (n + 1) / k)}
  _ = streamMeasure Î½
      {Ï‰ | âˆš(2 * c * Ïƒ2 * log (n + 1) / k) â‰¤ (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id])) / k} := by
    congr with Ï‰
    field_simp
    rw [Finset.sum_sub_distrib]
    simp
    grind
  _ = streamMeasure Î½
      {Ï‰ | âˆš(2 * c * k * Ïƒ2 * log (n + 1)) â‰¤ (âˆ‘ s âˆˆ range k, (Ï‰ s a - (Î½ a)[id]))} := by
    congr with Ï‰
    field_simp
    congr! 1
    rw [sqrt_div (by positivity), â† mul_div_assoc, mul_comm, mul_div_assoc, div_sqrt,
      mul_comm _ (k : â„), sqrt_mul (x := (k : â„)) (by positivity), mul_comm]
  _ â‰¤ 1 / (n + 1) ^ c := prob_sum_ge_sqrt_log hÎ½ hÏƒ2 hc a k hk

end Subgaussian

end Bandits
