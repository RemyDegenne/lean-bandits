/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import LeanBandits.Bandit.Bandit
import Mathlib.Probability.IdentDistribIndep

/-! # Laws of `stepsUntil` and `rewardByCount`
-/

open MeasureTheory ProbabilityTheory Finset Learning
open scoped ENNReal NNReal

namespace Bandits

variable {Î± Î© : Type*} {mÎ± : MeasurableSpace Î±} {mÎ© : MeasurableSpace Î©} [DecidableEq Î±]
  [StandardBorelSpace Î±] [Nonempty Î±]
  {A : â„• â†’ Î© â†’ Î±} {R : â„• â†’ Î© â†’ â„} {P : Measure Î©} [IsProbabilityMeasure P]
  {alg : Algorithm Î± â„} {Î½ : Kernel Î± â„} [IsMarkovKernel Î½]
  {h_inter : IsAlgEnvSeq A R alg (stationaryEnv Î½) P}

local notation "ğ”“" => P.prod (streamMeasure Î½)

omit [DecidableEq Î±] [StandardBorelSpace Î±] [Nonempty Î±] in
lemma hasLaw_Z (a : Î±) (m : â„•) :
  HasLaw (fun Ï‰ â†¦ Ï‰.2 m a) (Î½ a) ğ”“ where
  map_eq := by
    calc (ğ”“).map (fun Ï‰ â†¦ Ï‰.2 m a)
    _ = ((ğ”“).snd).map (fun Ï‰ â†¦ Ï‰ m a) := by
      rw [Measure.snd, Measure.map_map (by fun_prop) (by fun_prop)]
      rfl
    _ = (streamMeasure Î½).map (fun Ï‰ â†¦ Ï‰ m a) := by simp
    _ = ((Measure.infinitePi fun _ â†¦ Measure.infinitePi Î½).map (fun Ï‰ â†¦ Ï‰ m)).map
        (fun Ï‰ â†¦ Ï‰ a) := by
      rw [streamMeasure, Measure.map_map (by fun_prop) (by fun_prop)]
      rfl
    _ = Î½ a := by simp_rw [(measurePreserving_eval_infinitePi _ _).map_eq]

/-- Law of `Y` conditioned on the event `s`.-/
notation "ğ“›[" Y " | " s "; " Î¼ "]" => Measure.map Y (Î¼[|s])
/-- Law of `Y` conditioned on the event that `X` is in `s`. -/
notation "ğ“›[" Y " | " X " in " s "; " Î¼ "]" => Measure.map Y (Î¼[|X â»Â¹' s])
/-- Law of `Y` conditioned on the event that `X` equals `x`. -/
notation "ğ“›[" Y " | " X " â† " x "; " Î¼ "]" => Measure.map Y (Î¼[|X â»Â¹' {x}])

omit [DecidableEq Î±] in
lemma condDistrib_reward'' [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (n : â„•) :
    ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ A n Ï‰.1; ğ”“] =áµ[(ğ”“).map (fun Ï‰ â†¦ A n Ï‰.1)] Î½ := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  have h_ra' : ğ“›[R n | A n; P] =áµ[P.map (A n)] Î½ := h.condDistrib_reward_stationaryEnv n
  have h_law : (ğ”“).map (fun Ï‰ â†¦ A n Ï‰.1) = P.map (A n) := by
    change ((ğ”“).map (A n âˆ˜ Prod.fst)) = _
    rw [â† Measure.map_map (by fun_prop) (by fun_prop), â† Measure.fst, Measure.fst_prod]
  rw [h_law]
  have h_prod : ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ A n Ï‰.1; ğ”“]
      =áµ[P.map (A n)] ğ“›[R n | A n; P] :=
    condDistrib_fst_prod _ (by fun_prop) _
  filter_upwards [h_ra', h_prod] with Ï‰ h_eq h_prod
  rw [h_prod, h_eq]

omit [DecidableEq Î±] in
lemma reward_cond_action [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (n : â„•)
    (hÎ¼a : (ğ”“).map (fun Ï‰ â†¦ A n Ï‰.1) {a} â‰  0) :
    ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ A n Ï‰.1 â† a; ğ”“] = Î½ a := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  have h_ra : ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ A n Ï‰.1; ğ”“] =áµ[(ğ”“).map (fun Ï‰ â†¦ A n Ï‰.1)] Î½ :=
    condDistrib_reward'' h n
  have h_eq := condDistrib_ae_eq_cond (Î¼ := ğ”“)
    (X := fun Ï‰ â†¦ A n Ï‰.1) (Y := fun Ï‰ â†¦ R n Ï‰.1) (by fun_prop) (by fun_prop)
  rw [Filter.EventuallyEq, ae_iff_of_countable] at h_ra h_eq
  specialize h_ra a hÎ¼a
  specialize h_eq a hÎ¼a
  rw [h_ra] at h_eq
  exact h_eq.symm

lemma condIndepFun_reward_stepsUntil_action' [StandardBorelSpace Î©]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (m n : â„•) :
    R n âŸ‚áµ¢[A n, h.measurable_A n; P] {Ï‰ | stepsUntil A a m Ï‰ = â†‘n}.indicator (fun _ â†¦ 1) := by
  -- the indicator of `stepsUntil ... = n` is a function of `hist (n-1)` and `action n`.
  -- It thus suffices to use the independence of `reward n` and `hist (n-1)` conditionally
  -- on `action n`.
  have hA := h.measurable_A
  have hR := h.measurable_R
  by_cases hn : n = 0
  Â· have h_indep : R 0 âŸ‚áµ¢[A 0, hA 0; P] A 0 :=
      condIndepFun_self_right (by fun_prop) (by fun_prop)
    simp only [hn, CharP.cast_eq_zero]
    refine h_indep.of_measurable_right (hX := hA 0) ?_
    exact measurable_comap_indicator_stepsUntil_eq_zero a m
  Â· have h_indep : R n âŸ‚áµ¢[A n, hA n; P] fun Ï‰ â†¦ (IsAlgEnvSeq.hist A R (n - 1) Ï‰, A n Ï‰) :=
      IsAlgEnvSeq.condIndepFun_reward_hist_action_action' h n (by grind)
    refine h_indep.of_measurable_right (hX := hA n) ?_
    exact measurable_comap_indicator_stepsUntil_eq hA hR a m n

lemma condIndepFun_reward_stepsUntil_action [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P)
    (a : Î±) (m n : â„•) :
    CondIndepFun (mÎ±.comap (fun Ï‰ â†¦ A n Ï‰.1)) ((h.measurable_A n).comp measurable_fst).comap_le
      (fun Ï‰ â†¦ R n Ï‰.1) ({Ï‰ | stepsUntil A a m Ï‰.1 = â†‘n}.indicator (fun _ â†¦ 1)) ğ”“ := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  exact condIndepFun_fst_prod (Î½ := streamMeasure Î½)
    (measurable_indicator_stepsUntil_eq hA hR a m n) (by fun_prop) (by fun_prop)
    (condIndepFun_reward_stepsUntil_action' h a m n)

lemma reward_cond_stepsUntil [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (m n : â„•)
    (hm : m â‰  0) (hÎ¼n : ğ”“ ((fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) â»Â¹' {â†‘n}) â‰  0) :
    ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ stepsUntil A a m Ï‰.1 â† â†‘n; ğ”“] = Î½ a := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  have hÎ¼na :
      ğ”“ ((fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) â»Â¹' {â†‘n} âˆ© (fun Ï‰ â†¦ A n Ï‰.1) â»Â¹' {a}) â‰  0 := by
    suffices ((fun Ï‰ : Î© Ã— (â„• â†’ Î± â†’ â„) â†¦
          stepsUntil A a m Ï‰.1) â»Â¹' {â†‘n} âˆ© (fun Ï‰ â†¦ A n Ï‰.1) â»Â¹' {a})
        = (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) â»Â¹' {â†‘n} by simpa [this] using hÎ¼n
    ext Ï‰
    simp only [Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff, and_iff_left_iff_imp]
    exact action_eq_of_stepsUntil_eq_coe hm
  have hÎ¼a : (ğ”“).map (fun Ï‰ â†¦ A n Ï‰.1) {a} â‰  0 := by
    rw [Measure.map_apply (by fun_prop) (measurableSet_singleton _)]
    refine fun h_zero â†¦ hÎ¼n (measure_mono_null (fun Ï‰ â†¦ ?_) h_zero)
    simp only [Set.mem_preimage, Set.mem_singleton_iff]
    exact action_eq_of_stepsUntil_eq_coe hm
  calc ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ stepsUntil A a m Ï‰.1 â† (n : â„•âˆ); ğ”“]
  _ = (ğ”“[|(fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) â»Â¹' {â†‘n} âˆ© (fun Ï‰ â†¦ A n Ï‰.1) â»Â¹' {a}]).map
      (fun Ï‰ â†¦ R n Ï‰.1) := by
    congr with Ï‰
    simp only [Set.mem_preimage, Set.mem_singleton_iff, Set.mem_inter_iff, iff_self_and]
    exact action_eq_of_stepsUntil_eq_coe hm
  _ = (ğ”“[|(fun Ï‰ â†¦ A n Ï‰.1) â»Â¹' {a}
      âˆ© {Ï‰ : Î© Ã— (â„• â†’ Î± â†’ â„) | stepsUntil A a m Ï‰.1 = â†‘n}.indicator 1 â»Â¹' {1} ]).map
      (fun Ï‰ â†¦ R n Ï‰.1) := by
    congr 2 with Ï‰
    simp only [Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff, Set.indicator_apply,
      Set.mem_setOf_eq, Pi.one_apply, ite_eq_left_iff, zero_ne_one, imp_false, Decidable.not_not]
    rw [and_comm]
  _ = ğ“›[fun Ï‰ â†¦ R n Ï‰.1 | fun Ï‰ â†¦ A n Ï‰.1 â† a; ğ”“] := by
    rw [cond_of_condIndepFun (by fun_prop)]
    Â· exact condIndepFun_reward_stepsUntil_action h a m n
    Â· refine measurable_one.indicator ?_
      exact measurableSet_eq_fun (by fun_prop) (by fun_prop)
    Â· fun_prop
    Â· convert hÎ¼na using 2
      rw [Set.inter_comm]
      congr 1 with Ï‰
      simp [Set.indicator_apply]
  _ = Î½ a := reward_cond_action h a n hÎ¼a

/-- The conditional distribution of the reward received at the `m`-th pull of action `a`
given the time at which number of pulls is `m` is the constant kernel with value `Î½ a`. -/
theorem condDistrib_rewardByCount_stepsUntil [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (m : â„•) (hm : m â‰  0) :
    condDistrib (rewardByCount A R a m) (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) ğ”“
      =áµ[(ğ”“).map (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1)] Kernel.const _ (Î½ a) := by
  have hA := h.measurable_A
  have hR := h.measurable_R
  refine (condDistrib_ae_eq_cond (Î¼ := ğ”“)
    (X := fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) (by fun_prop) (by fun_prop)).trans ?_
  rw [Filter.EventuallyEq, ae_iff_of_countable]
  intro n hn
  simp only [Kernel.const_apply]
  cases n with
  | top =>
    rw [Measure.map_congr (g := fun Ï‰ â†¦ Ï‰.2 m a)]
    swap
    Â· refine ae_cond_of_forall_mem ((measurableSet_singleton _).preimage (by fun_prop)) ?_
      simp only [Set.mem_preimage, Set.mem_singleton_iff]
      exact fun Ï‰ â†¦ rewardByCount_of_stepsUntil_eq_top
    rw [cond_of_indepFun _ (by fun_prop) (by fun_prop) (measurableSet_singleton _)]
    Â· exact (hasLaw_Z a m).map_eq
    Â· rwa [Measure.map_apply (by fun_prop) (measurableSet_singleton _)] at hn
    Â· exact indepFun_prod (X := fun Ï‰ : Î© â†¦ stepsUntil A a m Ï‰)
        (Y := fun Ï‰ : â„• â†’ Î± â†’ â„ â†¦ Ï‰ m a) (by fun_prop) (by fun_prop)
  | coe n =>
    rw [Measure.map_congr (g := fun Ï‰ â†¦ R n Ï‰.1)]
    swap
    Â· refine ae_cond_of_forall_mem ((measurableSet_singleton _).preimage (by fun_prop)) ?_
      simp only [Set.mem_preimage, Set.mem_singleton_iff]
      exact fun Ï‰ â†¦ rewardByCount_of_stepsUntil_eq_coe
    refine reward_cond_stepsUntil h a m n hm ?_
    rwa [Measure.map_apply (by fun_prop) (measurableSet_singleton _)] at hn

/-- The reward received at the `m`-th pull of action `a` has law `Î½ a`. -/
lemma hasLaw_rewardByCount [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (m : â„•) (hm : m â‰  0) :
    HasLaw (rewardByCount A R a m) (Î½ a) ğ”“ where
  aemeasurable := (measurable_rewardByCount h.measurable_A h.measurable_R a m).aemeasurable
  map_eq := by
    have hA := h.measurable_A
    have hR := h.measurable_R
    have h_condDistrib :
        condDistrib (rewardByCount A R a m) (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) ğ”“
        =áµ[(ğ”“).map (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1)]
          Kernel.const _ (Î½ a) := condDistrib_rewardByCount_stepsUntil h a m hm
    calc (ğ”“).map (rewardByCount A R a m)
    _ = (condDistrib (rewardByCount A R a m) (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1) ğ”“)
        âˆ˜â‚˜ ((ğ”“).map (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1)) := by
      rw [condDistrib_comp_map (by fun_prop) (by fun_prop)]
    _ = (Kernel.const _ (Î½ a)) âˆ˜â‚˜ ((ğ”“).map (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1)) :=
      Measure.comp_congr h_condDistrib
    _ = Î½ a := by
      have : IsProbabilityMeasure ((ğ”“).map (fun Ï‰ â†¦ stepsUntil A a m Ï‰.1)) :=
        Measure.isProbabilityMeasure_map (by fun_prop)
      simp

lemma identDistrib_rewardByCount [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (n m : â„•)
    (hn : n â‰  0) (hm : m â‰  0) :
    IdentDistrib (rewardByCount A R a n) (rewardByCount A R a m) ğ”“ ğ”“ where
  aemeasurable_fst := (measurable_rewardByCount h.measurable_A h.measurable_R a n).aemeasurable
  aemeasurable_snd := (measurable_rewardByCount h.measurable_A h.measurable_R a m).aemeasurable
  map_eq := by rw [(hasLaw_rewardByCount h a n hn).map_eq, (hasLaw_rewardByCount h a m hm).map_eq]

lemma identDistrib_rewardByCount_id [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (n : â„•) (hn : n â‰  0) :
    IdentDistrib (rewardByCount A R a n) id ğ”“ (Î½ a) where
  aemeasurable_fst := (measurable_rewardByCount h.measurable_A h.measurable_R a n).aemeasurable
  aemeasurable_snd := Measurable.aemeasurable <| by fun_prop
  map_eq := by rw [(hasLaw_rewardByCount h a n hn).map_eq, Measure.map_id]

lemma identDistrib_rewardByCount_eval [StandardBorelSpace Î©] [Countable Î±]
    (h : IsAlgEnvSeq A R alg (stationaryEnv Î½) P) (a : Î±) (n m : â„•) (hn : n â‰  0) :
    IdentDistrib (rewardByCount A R a n) (fun Ï‰ â†¦ Ï‰ m a) ğ”“ (streamMeasure Î½) :=
  (identDistrib_rewardByCount_id h a n hn).trans
    (identDistrib_eval_eval_id_streamMeasure Î½ m a).symm

end Bandits
