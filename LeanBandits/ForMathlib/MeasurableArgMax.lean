/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Constructions.BorelSpace.Order

/-! # Measurable argmax function

-/

open MeasureTheory Finset
open scoped ENNReal NNReal

section MeasurableArgmax -- copied from PR #27579 (and changed from argmin to argmax)

lemma measurable_encode {Î± : Type*} {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    Measurable (Encodable.encode (Î± := Î±)) := by
  refine measurable_to_nat fun a â†¦ ?_
  rw [show Encodable.encode â»Â¹' {Encodable.encode a} = {a} from by ext; simp]; measurability

lemma measurableEmbedding_encode (Î± : Type*) {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    MeasurableEmbedding (Encodable.encode (Î± := Î±)) where
  injective := Encodable.encode_injective
  measurable := measurable_encode
  measurableSet_image' _ _ := .of_discrete

section Finite

variable {ğ“§ ğ“¨ Î± : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {mÎ± : MeasurableSpace Î±} [TopologicalSpace Î±] [LinearOrder Î±]
  [OpensMeasurableSpace Î±] [OrderClosedTopology Î±] [SecondCountableTopology Î±]

lemma measurableSet_isMax [Countable ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±} (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) (y : ğ“¨) :
    MeasurableSet {x | âˆ€ z, f x z â‰¤ f x y} := by
  rw [show {x | âˆ€ y', f x y' â‰¤ f x y} = â‹‚ y', {x | f x y' â‰¤ f x y} by ext; simp]
  exact .iInter fun z â†¦ measurableSet_le (hf z) (hf y)

lemma exists_isMaxOn' {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] (f : ğ“§ â†’ ğ“¨ â†’ Î±) (x : ğ“§) :
    âˆƒ n : â„•, âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ z, f x z â‰¤ f x y :=
  let âŸ¨y, hâŸ© := Finite.exists_max (f x); âŸ¨Encodable.encode y, y, rfl, hâŸ©

/-- A measurable argmax function. -/
noncomputable
def measurableArgmax [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (x : ğ“§) :
    ğ“¨ :=
  (measurableEmbedding_encode ğ“¨).invFun (Nat.find (exists_isMaxOn' f x))

lemma measurable_measurableArgmax [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±}
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) :
    Measurable (measurableArgmax f) := by
  refine (MeasurableEmbedding.measurable_invFun (measurableEmbedding_encode ğ“¨)).comp
    (measurable_find _ fun n â†¦ ?_)
  rw [show {x | âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y}
      = â‹ƒ y, ({x | n = Encodable.encode y} âˆ© {x | âˆ€ z, f x z â‰¤ f x y}) from by ext; simp]
  exact .iUnion fun y â†¦ .inter (by simp) (measurableSet_isMax hf y)

lemma isMaxOn_measurableArgmax {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (x : ğ“§) (z : ğ“¨) :
    f x z â‰¤ f x (measurableArgmax f x) := by
  obtain âŸ¨y, h_eq, h_leâŸ© := Nat.find_spec (exists_isMaxOn' f x)
  exact (h_le z).trans_eq <| by rw [measurableArgmax, h_eq,
    MeasurableEmbedding.leftInverse_invFun (measurableEmbedding_encode ğ“¨) y]

/-- Congruence lemma: measurableArgmax only depends on the function values at the point. -/
lemma measurableArgmax_congr {ğ“§â‚ ğ“§â‚‚ : Type*} {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (fâ‚ : ğ“§â‚ â†’ ğ“¨ â†’ Î±) (fâ‚‚ : ğ“§â‚‚ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), fâ‚ x z â‰¤ fâ‚ x y]
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), fâ‚‚ x z â‰¤ fâ‚‚ x y]
    (xâ‚ : ğ“§â‚) (xâ‚‚ : ğ“§â‚‚) (h : fâ‚ xâ‚ = fâ‚‚ xâ‚‚) :
    measurableArgmax fâ‚ xâ‚ = measurableArgmax fâ‚‚ xâ‚‚ := by
  simp only [measurableArgmax]; congr 1
  exact Nat.find_congr' fun {_} =>
    âŸ¨fun âŸ¨y, hn, hyâŸ© => âŸ¨y, hn, h â–¸ hyâŸ©, fun âŸ¨y, hn, hyâŸ© => âŸ¨y, hn, h.symm â–¸ hyâŸ©âŸ©

/-- measurableArgmax is independent of the DecidablePred instance used.
    This follows from Nat.find_congr' which handles different decidability instances. -/
lemma measurableArgmax_eq_of_eq {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    (d1 : âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y)
    (d2 : âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y)
    (x : ğ“§) :
    @measurableArgmax ğ“§ ğ“¨ Î± _ _ _ _ _ _ f d1 x = @measurableArgmax ğ“§ ğ“¨ Î± _ _ _ _ _ _ f d2 x := by
  simp only [measurableArgmax]; congr 1
  exact @Nat.find_congr' _ _ (d1 x) (d2 x) _ _ (fun {_} â†¦ Iff.rfl)

end Finite
end MeasurableArgmax
