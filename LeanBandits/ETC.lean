/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Moments.SubGaussian
import LeanBandits.Bandit
import LeanBandits.Regret

/-! # The Explore-Then-Commit Algorithm

-/

open MeasureTheory ProbabilityTheory Finset
open scoped ENNReal NNReal

section MeasurableArgmax -- copied from PR #27579 (and changed from argmin to argmax)

lemma measurable_encode {Î± : Type*} {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    Measurable (Encodable.encode (Î± := Î±)) := by
  refine measurable_to_nat fun a â†¦ ?_
  have : Encodable.encode â»Â¹' {Encodable.encode a} = {a} := by ext; simp
  rw [this]
  exact measurableSet_singleton _

lemma measurableEmbedding_encode (Î± : Type*) {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    MeasurableEmbedding (Encodable.encode (Î± := Î±)) where
  injective := Encodable.encode_injective
  measurable := measurable_encode
  measurableSet_image' _ _ := .of_discrete

section Finite

variable {ğ“§ ğ“¨ Î± : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {mÎ± : MeasurableSpace Î±} [TopologicalSpace Î±] [LinearOrder Î±]
  [OpensMeasurableSpace Î±] [OrderClosedTopology Î±] [SecondCountableTopology Î±]

lemma measurableSet_isMax [Countable ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±} (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) (y : ğ“¨) :
    MeasurableSet {x | âˆ€ z, f x z â‰¤ f x y} := by
  rw [show {x | âˆ€ y', f x y' â‰¤ f x y} = â‹‚ y', {x | f x y' â‰¤ f x y} by ext; simp]
  exact MeasurableSet.iInter fun z â†¦ measurableSet_le (by fun_prop) (by fun_prop)

lemma exists_isMaxOn' {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] (f : ğ“§ â†’ ğ“¨ â†’ Î±) (x : ğ“§) :
    âˆƒ n : â„•, âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ z, f x z â‰¤ f x y := by
  obtain âŸ¨y, hâŸ© := Finite.exists_max (f x)
  exact âŸ¨Encodable.encode y, y, rfl, hâŸ©

/-- A measurable argmax function. -/
noncomputable
def measurableArgmax [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (x : ğ“§) :
    ğ“¨ :=
  (measurableEmbedding_encode ğ“¨).invFun (Nat.find (exists_isMaxOn' f x))

lemma measurable_measurableArgmax [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±}
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) :
    Measurable (measurableArgmax f) := by
  refine (MeasurableEmbedding.measurable_invFun (measurableEmbedding_encode ğ“¨)).comp ?_
  refine measurable_find _ fun n â†¦ ?_
  have : {x | âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y}
      = â‹ƒ y, ({x | n = Encodable.encode y} âˆ© {x | âˆ€ z, f x z â‰¤ f x y}) := by ext; simp
  rw [this]
  refine MeasurableSet.iUnion fun y â†¦ (MeasurableSet.inter (by simp) ?_)
  exact measurableSet_isMax (by fun_prop) y

lemma isMaxOn_measurableArgmax {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (x : ğ“§) (z : ğ“¨) :
    f x z â‰¤ f x (measurableArgmax f x) := by
  obtain âŸ¨y, h_eq, h_leâŸ© := Nat.find_spec (exists_isMaxOn' f x)
  refine le_trans (h_le z) (le_of_eq ?_)
  rw [measurableArgmax, h_eq,
    MeasurableEmbedding.leftInverse_invFun (measurableEmbedding_encode ğ“¨) y]

end Finite
end MeasurableArgmax

namespace Bandits

variable {K : â„•}

-- TODO: when defining the kernel of an algorithm, we use `Iic n â†’ Î± Ã— â„` as the history type.
-- But for all the defs in the regret file, we use `â„• â†’ Î± Ã— â„` as the history type.
-- Should we unify this?

/-- The empirical mean of arm `a` at time `n` but weighted by `m`.
We will use it only for `n = K * m - 1`, time for which there is indeed `m` samples for each arm,
but for reasons that have to do with type equalities we define it for arbitrary `n`. -/
noncomputable
def empMeanETC (m n : â„•) (h : Iic n â†’ Fin K Ã— â„) (a : Fin K) :=
  (âˆ‘ s : Iic n, if (h s).1 = a then (h s).2 else 0) / m

@[fun_prop]
lemma measurable_empMeanETC (m n : â„•) (a : Fin K) :
    Measurable (fun h â†¦ empMeanETC m n h a) := by
  unfold empMeanETC
  have h_meas s :
      Measurable (fun (h : Iic n â†’ Fin K Ã— â„) â†¦ if (h s).1 = a then (h s).2 else 0) := by
    refine Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  fun_prop

/-- Arm pulled by the ETC algorithm. -/
noncomputable
def etcArm (hK : 0 < K) (m n : â„•) (h : Iic n â†’ Fin K Ã— â„) : Fin K :=
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  if hn : n < K * m - 1 then
    âŸ¨(n + 1) % K, Nat.mod_lt _ hKâŸ© -- for `n = 0` we have pulled arm 0 already, and we pull arm 1
  else
    if hn_eq : n = K * m - 1 then measurableArgmax (empMeanETC m n) h
    else (h âŸ¨n - 1, by simpâŸ©).1

@[fun_prop]
lemma measurable_etcArm (hK : 0 < K) (m n : â„•) : Measurable (etcArm hK m n) := by
  have : Nonempty (Fin K) := Fin.pos_iff_nonempty.mp hK
  unfold etcArm
  simp only [dite_eq_ite]
  refine Measurable.ite (by simp) (by fun_prop) ?_
  refine Measurable.ite (by simp) ?_ (by fun_prop)
  exact measurable_measurableArgmax fun a â†¦ by fun_prop

/-- The Explore-Then-Commit Kernel, which describes the arm pulled by the ETC algorithm. -/
noncomputable
def etcKernel (hK : 0 < K) (m n : â„•) : Kernel (Iic n â†’ Fin K Ã— â„) (Fin K) :=
  Kernel.deterministic (etcArm hK m n) (by fun_prop)

instance (hK : 0 < K) (m n : â„•) : IsMarkovKernel (etcKernel hK m n) := by
  unfold etcKernel
  infer_instance

/-- The measure describing the first pull of the ETC algorithm. -/
noncomputable
def etcP0 (hK : 0 < K) : Measure (Fin K) := Measure.dirac âŸ¨0, hKâŸ©

instance (hK : 0 < K) : IsProbabilityMeasure (etcP0 hK) := by
  unfold etcP0
  infer_instance

/-- A bandit interaction between the ETC algorithm and an environment given by reward
distributions. -/
noncomputable
def ETCBandit (hK : 0 < K) (m : â„•) (Î½ : Kernel (Fin K) â„) [IsMarkovKernel Î½] : Bandit (Fin K) where
  Î½ := Î½
  policy := etcKernel hK m
  p0 := etcP0 hK

end Bandits
