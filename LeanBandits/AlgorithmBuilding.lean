/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import LeanBandits.Bandit

/-! # Tools to build bandit algorithms

-/

open MeasureTheory ProbabilityTheory Finset
open scoped ENNReal NNReal

section MeasurableArgmax -- copied from PR #27579 (and changed from argmin to argmax)

lemma measurable_encode {Î± : Type*} {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    Measurable (Encodable.encode (Î± := Î±)) := by
  refine measurable_to_nat fun a â†¦ ?_
  have : Encodable.encode â»Â¹' {Encodable.encode a} = {a} := by ext; simp
  rw [this]
  exact measurableSet_singleton _

lemma measurableEmbedding_encode (Î± : Type*) {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    MeasurableEmbedding (Encodable.encode (Î± := Î±)) where
  injective := Encodable.encode_injective
  measurable := measurable_encode
  measurableSet_image' _ _ := .of_discrete

section Finite

variable {ğ“§ ğ“¨ Î± : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {mÎ± : MeasurableSpace Î±} [TopologicalSpace Î±] [LinearOrder Î±]
  [OpensMeasurableSpace Î±] [OrderClosedTopology Î±] [SecondCountableTopology Î±]

lemma measurableSet_isMax [Countable ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±} (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) (y : ğ“¨) :
    MeasurableSet {x | âˆ€ z, f x z â‰¤ f x y} := by
  rw [show {x | âˆ€ y', f x y' â‰¤ f x y} = â‹‚ y', {x | f x y' â‰¤ f x y} by ext; simp]
  exact MeasurableSet.iInter fun z â†¦ measurableSet_le (by fun_prop) (by fun_prop)

lemma exists_isMaxOn' {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] (f : ğ“§ â†’ ğ“¨ â†’ Î±) (x : ğ“§) :
    âˆƒ n : â„•, âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ z, f x z â‰¤ f x y := by
  obtain âŸ¨y, hâŸ© := Finite.exists_max (f x)
  exact âŸ¨Encodable.encode y, y, rfl, hâŸ©

/-- A measurable argmax function. -/
noncomputable
def measurableArgmax [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (x : ğ“§) :
    ğ“¨ :=
  (measurableEmbedding_encode ğ“¨).invFun (Nat.find (exists_isMaxOn' f x))

lemma measurable_measurableArgmax [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±}
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) :
    Measurable (measurableArgmax f) := by
  refine (MeasurableEmbedding.measurable_invFun (measurableEmbedding_encode ğ“¨)).comp ?_
  refine measurable_find _ fun n â†¦ ?_
  have : {x | âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y}
      = â‹ƒ y, ({x | n = Encodable.encode y} âˆ© {x | âˆ€ z, f x z â‰¤ f x y}) := by ext; simp
  rw [this]
  refine MeasurableSet.iUnion fun y â†¦ (MeasurableSet.inter (by simp) ?_)
  exact measurableSet_isMax (by fun_prop) y

lemma isMaxOn_measurableArgmax {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x z â‰¤ f x y]
    (x : ğ“§) (z : ğ“¨) :
    f x z â‰¤ f x (measurableArgmax f x) := by
  obtain âŸ¨y, h_eq, h_leâŸ© := Nat.find_spec (exists_isMaxOn' f x)
  refine le_trans (h_le z) (le_of_eq ?_)
  rw [measurableArgmax, h_eq,
    MeasurableEmbedding.leftInverse_invFun (measurableEmbedding_encode ğ“¨) y]

end Finite
end MeasurableArgmax

namespace Bandits

variable {Î± : Type*} [DecidableEq Î±] [MeasurableSpace Î±]

/-- Number of pulls of arm `a` up to (and including) time `n`. -/
noncomputable
def pullCount' (n : â„•) (h : Iic n â†’ Î± Ã— â„) (a : Î±) := #{s | (h s).1 = a}

/-- Sum of rewards of arm `a` up to (and including) time `n`. -/
noncomputable
def sumRewards' (n : â„•) (h : Iic n â†’ Î± Ã— â„) (a : Î±) :=
  âˆ‘ s, if (h s).1 = a then (h s).2 else 0

/-- Empirical mean of arm `a` at time `n`. -/
noncomputable
def empMean' (n : â„•) (h : Iic n â†’ Î± Ã— â„) (a : Î±) :=
  (sumRewards' n h a) / (pullCount' n h a)

omit [MeasurableSpace Î±] in
lemma pullCount'_eq_sum (n : â„•) (h : Iic n â†’ Î± Ã— â„) (a : Î±) :
    pullCount' n h a = âˆ‘ s : Iic n, if (h s).1 = a then 1 else 0 := by simp [pullCount']

@[fun_prop]
lemma measurable_pullCount' [MeasurableSingletonClass Î±] (n : â„•) (a : Î±) :
    Measurable (fun h â†¦ pullCount' n h a) := by
  simp_rw [pullCount'_eq_sum]
  have h_meas s : Measurable (fun (h : Iic n â†’ Î± Ã— â„) â†¦ if (h s).1 = a then 1 else 0) := by
    refine Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  fun_prop

@[fun_prop]
lemma measurable_sumRewards' [MeasurableSingletonClass Î±] (n : â„•) (a : Î±) :
    Measurable (fun h â†¦ sumRewards' n h a) := by
  simp_rw [sumRewards']
  have h_meas s : Measurable (fun (h : Iic n â†’ Î± Ã— â„) â†¦ if (h s).1 = a then (h s).2 else 0) := by
    refine Measurable.ite ?_ (by fun_prop) (by fun_prop)
    exact (measurableSet_singleton _).preimage (by fun_prop)
  fun_prop

@[fun_prop]
lemma measurable_empMean' [MeasurableSingletonClass Î±] (n : â„•) (a : Î±) :
    Measurable (fun h â†¦ empMean' n h a) := by
  unfold empMean'
  fun_prop

end Bandits
