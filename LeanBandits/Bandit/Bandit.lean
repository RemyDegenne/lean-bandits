/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Paulo Rauber
-/
import LeanBandits.ForMathlib.IndepInfinitePi
import LeanBandits.SequentialLearning.Deterministic
import LeanBandits.SequentialLearning.StationaryEnv
import LeanBandits.SequentialLearning.FiniteActions
import Mathlib.Probability.IdentDistrib
import Mathlib.MeasureTheory.Constructions.UnitInterval

/-!
# Bandit
-/

open MeasureTheory ProbabilityTheory Filter Real Finset Learning

open scoped ENNReal NNReal

namespace Bandits

variable {Î± R : Type*} {mÎ± : MeasurableSpace Î±} {mR : MeasurableSpace R}

section MeasureSpace

namespace Bandit

/-- Kernel describing the distribution of the next arm-reward pair given the history up to `n`. -/
noncomputable
def stepKernel (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    Kernel (Iic n â†’ Î± Ã— R) (Î± Ã— R) :=
  Learning.stepKernel alg (stationaryEnv Î½) n
deriving IsMarkovKernel

@[simp]
lemma fst_stepKernel (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    (stepKernel alg Î½ n).fst = alg.policy n := by
  rw [stepKernel, Learning.fst_stepKernel]

@[simp]
lemma snd_stepKernel (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    (stepKernel alg Î½ n).snd = Î½ âˆ˜â‚– alg.policy n := by
  rw [stepKernel, Learning.stepKernel, stationaryEnv_feedback, Kernel.snd_compProd_prodMkLeft]

/-- Measure on the sequence of arms pulled and rewards observed generated by the bandit. -/
noncomputable
def trajMeasure (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] : Measure (â„• â†’ Î± Ã— R) :=
  Learning.trajMeasure alg (stationaryEnv Î½)
deriving IsProbabilityMeasure

/-- Measure of an infinite stream of rewards from each arm. -/
noncomputable
def streamMeasure (Î½ : Kernel Î± R) : Measure (â„• â†’ Î± â†’ R) :=
  Measure.infinitePi fun _ â†¦ Measure.infinitePi Î½

instance (Î½ : Kernel Î± R) [IsMarkovKernel Î½] : IsProbabilityMeasure (streamMeasure Î½) := by
  unfold streamMeasure
  infer_instance

/-- Joint distribution of the sequence of arm pulled and rewards, and a stream of independent
rewards from all arms. -/
noncomputable
def measure (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    Measure ((â„• â†’ Î± Ã— R) Ã— (â„• â†’ Î± â†’ R)) :=
  (trajMeasure alg Î½).prod (streamMeasure Î½)
deriving IsProbabilityMeasure

@[simp]
lemma fst_measure (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    (measure alg Î½).fst = trajMeasure alg Î½ := by
  rw [measure, Measure.fst_prod]

@[simp]
lemma snd_measure (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    (measure alg Î½).snd = streamMeasure Î½ := by
  rw [measure, Measure.snd_prod]

end Bandit

section StreamMeasure

lemma _root_.hasLaw_eval_infinitePi {Î¹ : Type*} {X : Î¹ â†’ Type*} {mX : âˆ€ i, MeasurableSpace (X i)}
  (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)] (i : Î¹) :
    HasLaw (Function.eval i) (Î¼ i) (Measure.infinitePi Î¼) where
  aemeasurable := Measurable.aemeasurable (by fun_prop)
  map_eq := by exact (measurePreserving_eval_infinitePi Î¼ i).map_eq

lemma hasLaw_eval_streamMeasure (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    HasLaw (fun h : â„• â†’ Î± â†’ R â†¦ h n) (Measure.infinitePi Î½) (Bandit.streamMeasure Î½) :=
  hasLaw_eval_infinitePi (fun _ â†¦ Measure.infinitePi Î½) n

lemma hasLaw_eval_eval_streamMeasure (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) (a : Î±) :
    HasLaw (fun h : â„• â†’ Î± â†’ R â†¦ h n a) (Î½ a) (Bandit.streamMeasure Î½) :=
  (hasLaw_eval_infinitePi Î½ a).comp (hasLaw_eval_streamMeasure Î½ n)

lemma identDistrib_eval_eval_id_streamMeasure (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) (a : Î±) :
    IdentDistrib (fun h : â„• â†’ Î± â†’ R â†¦ h n a) id (Bandit.streamMeasure Î½) (Î½ a) where
  aemeasurable_fst := Measurable.aemeasurable (by fun_prop)
  aemeasurable_snd := Measurable.aemeasurable (by fun_prop)
  map_eq := by
    rw [â† (hasLaw_eval_eval_streamMeasure Î½ n a).map_eq,
      Measure.map_map (by fun_prop) (by fun_prop)]
    simp

lemma Integrable.congr_identDistrib {Î© Î©' : Type*}
    {mÎ© : MeasurableSpace Î©} {mÎ©' : MeasurableSpace Î©'}
    {Î¼ : Measure Î©} {Î¼' : Measure Î©'} {X : Î© â†’ â„} {Y : Î©' â†’ â„}
    (hX : Integrable X Î¼) (hXY : IdentDistrib X Y Î¼ Î¼') :
    Integrable Y Î¼' := by
  have hX' : Integrable id (Î¼.map X) := by
    rwa [integrable_map_measure (by fun_prop) hXY.aemeasurable_fst]
  rw [hXY.map_eq] at hX'
  rwa [integrable_map_measure (by fun_prop) hXY.aemeasurable_snd] at hX'

lemma integrable_eval_streamMeasure (Î½ : Kernel Î± â„) [IsMarkovKernel Î½] (n : â„•) (a : Î±)
    (h_int : Integrable id (Î½ a)) :
    Integrable (fun h : â„• â†’ Î± â†’ â„ â†¦ h n a) (Bandit.streamMeasure Î½) :=
  Integrable.congr_identDistrib h_int (identDistrib_eval_eval_id_streamMeasure Î½ n a).symm

lemma integral_eval_streamMeasure (Î½ : Kernel Î± â„) [IsMarkovKernel Î½] (n : â„•) (a : Î±) :
    âˆ« h, h n a âˆ‚(Bandit.streamMeasure Î½) = (Î½ a)[id] := by
  calc âˆ« h, h n a âˆ‚(Bandit.streamMeasure Î½)
  _ = âˆ« x, x âˆ‚((Bandit.streamMeasure Î½).map (fun h â†¦ h n a)) := by
    rw [integral_map (Measurable.aemeasurable (by fun_prop)) (by fun_prop)]
  _ = (Î½ a)[id] := by simp [(hasLaw_eval_eval_streamMeasure Î½ n a).map_eq]

lemma iIndepFun_eval_streamMeasure' (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    iIndepFun (fun n Ï‰ â†¦ Ï‰ n) (Bandit.streamMeasure Î½) :=
  iIndepFun_infinitePi (P := fun (_ : â„•) â†¦ Measure.infinitePi Î½) (Î© := fun _ â†¦ Î± â†’ R)
    (X := fun i u â†¦ u) (fun i â†¦ by fun_prop)

lemma iIndepFun_eval_streamMeasure'' (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (a : Î±) :
    iIndepFun (fun n Ï‰ â†¦ Ï‰ n a) (Bandit.streamMeasure Î½) :=
  (iIndepFun_eval_streamMeasure' Î½).comp (g := fun i Ï‰ â†¦ Ï‰ a) (by fun_prop)

lemma iIndepFun_eval_streamMeasure (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    iIndepFun (fun (p : â„• Ã— Î±) Ï‰ â†¦ Ï‰ p.1 p.2) (Bandit.streamMeasure Î½) :=
  iIndepFun_uncurry_infinitePi' (X := fun _ _ â†¦ id) (fun _ â†¦ Î½) (by fun_prop)

lemma indepFun_eval_streamMeasure (Î½ : Kernel Î± R) [IsMarkovKernel Î½] {n m : â„•} {a b : Î±}
    (h : n â‰  m âˆ¨ a â‰  b) :
    IndepFun (fun Ï‰ â†¦ Ï‰ n a) (fun Ï‰ â†¦ Ï‰ m b) (Bandit.streamMeasure Î½) := by
  change IndepFun (fun Ï‰ â†¦ Ï‰ (n, a).1 (n, a).2) (fun Ï‰ â†¦ Ï‰ (m, b).1 (m, b).2)
    (Bandit.streamMeasure Î½)
  exact (iIndepFun_eval_streamMeasure Î½).indepFun (by grind)

lemma indepFun_eval_streamMeasure' (Î½ : Kernel Î± R) [IsMarkovKernel Î½] {a b : Î±} (h : a â‰  b) :
    IndepFun (fun Ï‰ n â†¦ Ï‰ n a) (fun Ï‰ n â†¦ Ï‰ n b) (Bandit.streamMeasure Î½) :=
  indepFun_proj_infinitePi_infinitePi h

lemma indepFun_eval_snd_measure (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½]
    {a b : Î±} (h : a â‰  b) :
    IndepFun (fun Ï‰ n â†¦ Ï‰.2 n a) (fun Ï‰ n â†¦ Ï‰.2 n b) (Bandit.measure alg Î½) := by
  refine indepFun_snd_prod ?_ ?_ (indepFun_eval_streamMeasure' Î½ h) (Bandit.trajMeasure alg Î½)
  Â· exact Measurable.aemeasurable (by fun_prop)
  Â· exact Measurable.aemeasurable (by fun_prop)

end StreamMeasure

/-- `arm n` is the arm pulled at time `n`. This is a random variable on the measurable space
`â„• â†’ Î± Ã— â„`. -/
def arm (n : â„•) (h : â„• â†’ Î± Ã— R) : Î± := (h n).1

/-- `reward n` is the reward at time `n`. This is a random variable on the measurable space
`â„• â†’ Î± Ã— R`. -/
def reward (n : â„•) (h : â„• â†’ Î± Ã— R) : R := (h n).2

/-- `hist n` is the history up to time `n`. This is a random variable on the measurable space
`â„• â†’ Î± Ã— R`. -/
def hist (n : â„•) (h : â„• â†’ Î± Ã— R) : Iic n â†’ Î± Ã— R := fun i â†¦ h i

@[fun_prop]
lemma measurable_arm (n : â„•) : Measurable (arm n (Î± := Î±) (R := R)) := measurable_action n

@[fun_prop]
lemma measurable_arm_prod : Measurable (fun p : â„• Ã— (â„• â†’ Î± Ã— R) â†¦ arm p.1 p.2) :=
  measurable_action_prod

@[fun_prop]
lemma measurable_reward (n : â„•) : Measurable (reward n (Î± := Î±) (R := R)) :=
  Learning.measurable_reward n

@[fun_prop]
lemma measurable_reward_prod : Measurable (fun p : â„• Ã— (â„• â†’ Î± Ã— R) â†¦ reward p.1 p.2) :=
  Learning.measurable_reward_prod

@[fun_prop]
lemma measurable_hist (n : â„•) : Measurable (hist n (Î± := Î±) (R := R)) :=
  Learning.measurable_hist n

lemma hist_eq_frestrictLe :
    hist = Preorder.frestrictLe (Â«Ï€Â» := fun _ â†¦ Î± Ã— R) := by
  ext n h i : 3
  simp [hist, Preorder.frestrictLe]

/-- Filtration of the bandit process. -/
protected def filtration (Î± R : Type*) [MeasurableSpace Î±] [MeasurableSpace R] :
    Filtration â„• (inferInstance : MeasurableSpace (â„• â†’ Î± Ã— R)) :=
  MeasureTheory.Filtration.piLE (X := fun _ â†¦ Î± Ã— R)

section Laws

lemma hasLaw_step_zero (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    HasLaw (fun h : â„• â†’ Î± Ã— R â†¦ h 0) (alg.p0 âŠ—â‚˜ Î½) (Bandit.trajMeasure alg Î½) :=
  Learning.hasLaw_step_zero alg (stationaryEnv Î½)

lemma hasLaw_arm_zero (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    HasLaw (arm 0) alg.p0 (Bandit.trajMeasure alg Î½) :=
  Learning.hasLaw_action_zero alg (stationaryEnv Î½)

lemma condDistrib_arm_reward [StandardBorelSpace Î±] [Nonempty Î±] [StandardBorelSpace R] [Nonempty R]
    (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    condDistrib (fun h â†¦ (arm (n + 1) h, reward (n + 1) h)) (hist n) (Bandit.trajMeasure alg Î½)
      =áµ[(Bandit.trajMeasure alg Î½).map (hist n)] Bandit.stepKernel alg Î½ n :=
  Learning.condDistrib_step alg (stationaryEnv Î½) n

lemma condDistrib_reward' [StandardBorelSpace Î±] [Nonempty Î±] [StandardBorelSpace R] [Nonempty R]
    (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    condDistrib (reward (n + 1)) (fun Ï‰ â†¦ (hist n Ï‰, arm (n + 1) Ï‰)) (Bandit.trajMeasure alg Î½)
      =áµ[(Bandit.trajMeasure alg Î½).map (fun Ï‰ â†¦ (hist n Ï‰, arm (n + 1) Ï‰))] Î½.prodMkLeft _ :=
  Learning.condDistrib_reward alg (stationaryEnv Î½) n

lemma condDistrib_reward [StandardBorelSpace Î±] [Nonempty Î±] [StandardBorelSpace R] [Nonempty R]
    (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    condDistrib (reward n) (arm n) (Bandit.trajMeasure alg Î½)
      =áµ[(Bandit.trajMeasure alg Î½).map (arm n)] Î½ :=
   Learning.condDistrib_reward_stationaryEnv n

lemma condDistrib_arm [StandardBorelSpace Î±] [Nonempty Î±] [StandardBorelSpace R] [Nonempty R]
    (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    condDistrib (arm (n + 1)) (hist n) (Bandit.trajMeasure alg Î½)
      =áµ[(Bandit.trajMeasure alg Î½).map (hist n)] alg.policy n :=
  Learning.condDistrib_action alg (stationaryEnv Î½) n

/-- The reward at time `n+1` is independent of the history up to time `n` given the arm at `n+1`. -/
lemma condIndepFun_reward_hist_arm [StandardBorelSpace Î±] [Nonempty Î±]
    [StandardBorelSpace R] [Nonempty R]
    {alg : Algorithm Î± R} {Î½ : Kernel Î± R} [IsMarkovKernel Î½] (n : â„•) :
    CondIndepFun (MeasurableSpace.comap (arm (n + 1)) inferInstance)
      (measurable_arm _).comap_le (reward (n + 1)) (hist n) (Bandit.trajMeasure alg Î½) :=
  Learning.condIndepFun_reward_hist_action n

lemma condIndepFun_reward_hist_arm_arm [StandardBorelSpace Î±] [Countable Î±] [Nonempty Î±]
    [StandardBorelSpace R] [Nonempty R]
    {alg : Algorithm Î± R} {Î½ : Kernel Î± R} [IsMarkovKernel Î½] (n : â„•) :
    reward (n + 1) âŸ‚áµ¢[arm (n + 1), measurable_arm (n + 1); Bandit.trajMeasure alg Î½]
      (fun Ï‰ â†¦ (hist n Ï‰, arm (n + 1) Ï‰)) := by
  have h_indep : reward (n + 1) âŸ‚áµ¢[arm (n + 1), measurable_arm (n + 1); Bandit.trajMeasure alg Î½]
      hist n := by
    convert condIndepFun_reward_hist_arm (alg := alg) (Î½ := Î½) n
  exact h_indep.prod_right (by fun_prop) (by fun_prop) (by fun_prop)

lemma condIndepFun_reward_hist_arm_arm' [StandardBorelSpace Î±] [Countable Î±] [Nonempty Î±]
    [StandardBorelSpace R] [Nonempty R]
    {alg : Algorithm Î± R} {Î½ : Kernel Î± R} [IsMarkovKernel Î½] (n : â„•) (hn : n â‰  0) :
    reward n âŸ‚áµ¢[arm n, measurable_arm n; Bandit.trajMeasure alg Î½]
      (fun Ï‰ â†¦ (hist (n - 1) Ï‰, arm n Ï‰)) := by
  have := condIndepFun_reward_hist_arm_arm (alg := alg) (Î½ := Î½) (n - 1)
  grind

end Laws

section DetAlgorithm

variable {nextArm : (n : â„•) â†’ (Iic n â†’ Î± Ã— R) â†’ Î±} {h_next : âˆ€ n, Measurable (nextArm n)}
  {arm0 : Î±} {Î½ : Kernel Î± R} [IsMarkovKernel Î½]

local notation "ð”“t" => Bandit.trajMeasure (detAlgorithm nextArm h_next arm0) Î½

lemma HasLaw_arm_zero_detAlgorithm : HasLaw (arm 0) (Measure.dirac arm0) ð”“t where
  map_eq := (hasLaw_arm_zero _ _).map_eq

lemma arm_zero_detAlgorithm [MeasurableSingletonClass Î±] :
    arm 0 =áµ[ð”“t] fun _ â†¦ arm0 :=
  Learning.action_zero_detAlgorithm

lemma arm_detAlgorithm_ae_eq [StandardBorelSpace Î±] [Nonempty Î±]
    [StandardBorelSpace R] [Nonempty R] (n : â„•) :
    arm (n + 1) =áµ[ð”“t] fun h â†¦ nextArm n (fun i â†¦ h i) :=
  Learning.action_detAlgorithm_ae_eq n

example [StandardBorelSpace Î±] [Nonempty Î±]
    [StandardBorelSpace R] [Nonempty R] :
    âˆ€áµ h âˆ‚(ð”“t), arm 0 h = arm0 âˆ§ âˆ€ n, arm (n + 1) h = nextArm n (fun i â†¦ h i) := by
  rw [eventually_and, ae_all_iff]
  exact âŸ¨arm_zero_detAlgorithm, arm_detAlgorithm_ae_eqâŸ©

end DetAlgorithm

section ArrayModel

open unitInterval

section Aux

-- from Mathlib PR #30112
theorem representation {Î± Î² : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}
    [Nonempty Î²] [StandardBorelSpace Î²]
    (Îº : Kernel Î± Î²) [IsMarkovKernel Îº] :
    âˆƒ (f : Î± â†’ I â†’ Î²), Measurable (Function.uncurry f) âˆ§ âˆ€ a, volume.map (f a) = Îº a := sorry

theorem representation_measure {Î² : Type*} {mÎ² : MeasurableSpace Î²}
    [Nonempty Î²] [StandardBorelSpace Î²]
    (Î¼ : Measure Î²) [IsProbabilityMeasure Î¼] :
    âˆƒ (f : I â†’ Î²), Measurable f âˆ§ volume.map f = Î¼ := by
  obtain âŸ¨f, hf_meas, hf_mapâŸ© := representation (Kernel.const Unit Î¼)
  specialize hf_map âŸ¨âŸ©
  exact âŸ¨f âŸ¨âŸ©, by fun_prop, by simpaâŸ©

end Aux

variable (Î± R) in
def probSpace : Type _ := (â„• â†’ I) Ã— (â„• â†’ Î± â†’ R)

instance {Î± R : Type*} [MeasurableSpace R] : MeasurableSpace (probSpace Î± R) :=
  inferInstanceAs (MeasurableSpace ((â„• â†’ I) Ã— (â„• â†’ Î± â†’ R)))

noncomputable
def arrayMeasure (Î½ : Kernel Î± R) : Measure (probSpace Î± R) :=
  (Measure.infinitePi fun _ â†¦ volume).prod (Bandit.streamMeasure Î½)

instance (Î½ : Kernel Î± R) [IsMarkovKernel Î½] : IsProbabilityMeasure (arrayMeasure Î½) :=
  Measure.prod.instIsProbabilityMeasure _ _

variable [Nonempty Î±] [StandardBorelSpace Î±]

noncomputable
def initAlgFunction (alg : Algorithm Î± R) : I â†’ Î± :=
  (representation_measure alg.p0).choose

lemma initAlgFunction_map (alg : Algorithm Î± R) : volume.map (initAlgFunction alg) = alg.p0 :=
  (representation_measure alg.p0).choose_spec.2

@[fun_prop]
lemma measurable_initAlgFunction (alg : Algorithm Î± R) :
    Measurable (initAlgFunction alg) := (representation_measure alg.p0).choose_spec.1

noncomputable
def algFunction (alg : Algorithm Î± R) (n : â„•) :
    (Iic n â†’ Î± Ã— R) â†’ I â†’ Î± :=
  (representation (alg.policy n)).choose

lemma algFunction_map (alg : Algorithm Î± R) (n : â„•) (h : Iic n â†’ Î± Ã— R) :
      volume.map (algFunction alg n h) = alg.policy n h :=
  (representation (alg.policy n)).choose_spec.2 h

@[fun_prop]
lemma measurable_algFunction (alg : Algorithm Î± R) (n : â„•) :
    Measurable (Function.uncurry (algFunction alg n)) :=
  (representation (alg.policy n)).choose_spec.1

noncomputable
def altHist [DecidableEq Î±] (alg : Algorithm Î± R) (Ï‰ : probSpace Î± R) : (n : â„•) â†’ Iic n â†’ Î± Ã— R
| 0 => fun _ â†¦ (initAlgFunction alg (Ï‰.1 0), Ï‰.2 0 (initAlgFunction alg (Ï‰.1 0)))
| n + 1 =>
  let hn : Iic n â†’ Î± Ã— R := altHist alg Ï‰ n
  let a : Î± := algFunction alg n hn (Ï‰.1 (n + 1))
  fun i â†¦ if hin : i â‰¤ n then hn âŸ¨i, by simp [hin]âŸ© else (a, Ï‰.2 (pullCount' n hn a + 1) a)

@[simp]
lemma altHist_zero [DecidableEq Î±] (alg : Algorithm Î± R) (Ï‰ : probSpace Î± R) :
    altHist alg Ï‰ 0 = fun _ â†¦ (initAlgFunction alg (Ï‰.1 0), Ï‰.2 0 (initAlgFunction alg (Ï‰.1 0))) :=
  rfl

lemma altHist_add_one [DecidableEq Î±] (alg : Algorithm Î± R) (Ï‰ : probSpace Î± R) (n : â„•) :
    let a : Î± := algFunction alg n (altHist alg Ï‰ n) (Ï‰.1 (n + 1))
    altHist alg Ï‰ (n + 1) =
      fun (i : Iic (n + 1)) â†¦ if hin : i â‰¤ n then altHist alg Ï‰ n âŸ¨i, by simp [hin]âŸ©
        else (a, Ï‰.2 (pullCount' n (altHist alg Ï‰ n) a + 1) a) :=
  rfl

lemma altHist_eq [DecidableEq Î±] (alg : Algorithm Î± R) (Ï‰ : probSpace Î± R) (n : â„•) :
    altHist alg Ï‰ n = fun i : Iic n â†¦ altHist alg Ï‰ i âŸ¨i.1, by simpâŸ© := by
  induction n with
  | zero =>
    ext i : 1
    simp only [altHist]
    sorry
  | succ n hn =>
    ext i : 1
    by_cases hin : i â‰¤ n
    Â· rw [altHist_add_one]
      simp only [hin, â†“reduceDIte]
      rw [funext_iff] at hn
      simp_rw [hn]
    Â· grind

@[fun_prop]
lemma measurable_altHist [DecidableEq Î±] (alg : Algorithm Î± R) (n : â„•) :
    Measurable (fun Ï‰ â†¦ altHist alg Ï‰ n) := by
  induction n with
  | zero =>
    simp_rw [altHist_zero, measurable_pi_iff]
    refine fun _ â†¦ Measurable.prodMk (by fun_prop) ?_
    sorry
  | succ n hn =>
    refine measurable_pi_iff.mpr fun i â†¦ ?_
    by_cases hin : i â‰¤ n
    Â· simp only [altHist, hin, â†“reduceDIte]
      rw [measurable_pi_iff] at hn
      exact hn âŸ¨i.1, by simp [hin]âŸ©
    Â· simp only [altHist, hin, â†“reduceDIte]
      refine Measurable.prodMk (by fun_prop) ?_
      sorry

noncomputable
def altArm [DecidableEq Î±] (alg : Algorithm Î± R) (n : â„•) (Ï‰ : probSpace Î± R) : Î± :=
  (altHist alg Ï‰ n âŸ¨n, by simpâŸ©).1

lemma altArm_zero [DecidableEq Î±] (alg : Algorithm Î± R) :
    altArm alg 0 = fun Ï‰ â†¦ initAlgFunction alg (Ï‰.1 0) := by
  ext
  simp [altArm, altHist_zero]

@[fun_prop]
lemma measurable_altArm [DecidableEq Î±] (alg : Algorithm Î± R) (n : â„•) :
    Measurable (altArm alg n) := by unfold altArm; fun_prop

noncomputable
def altReward [DecidableEq Î±] (alg : Algorithm Î± R) (n : â„•) (Ï‰ : probSpace Î± R) : R :=
  (altHist alg Ï‰ n âŸ¨n, by simpâŸ©).2

lemma altReward_zero [DecidableEq Î±] (alg : Algorithm Î± R) :
    altReward alg 0 = fun Ï‰ â†¦ Ï‰.2 0 (altArm alg 0 Ï‰) := by
  ext
  simp [altReward, altHist_zero, altArm_zero]

@[fun_prop]
lemma measurable_altReward [DecidableEq Î±] (alg : Algorithm Î± R) (n : â„•) :
    Measurable (altReward alg n) := by unfold altReward; fun_prop

variable [DecidableEq Î±]

lemma hasLaw_altArm_zero (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    HasLaw (altArm alg 0) alg.p0 (arrayMeasure Î½) where
  map_eq := by
    calc (arrayMeasure Î½).map (fun Ï‰ â†¦ initAlgFunction alg (Ï‰.1 0))
    _ = ((arrayMeasure Î½).fst.map (Function.eval 0)).map (initAlgFunction alg) := by
      rw [Measure.fst, Measure.map_map (by fun_prop) (by fun_prop),
        Measure.map_map (by fun_prop) (by fun_prop)]
      rfl
    _ = (volume : Measure I).map (initAlgFunction alg) := by
      simp only [arrayMeasure, Measure.fst_prod]
      rw [(measurePreserving_eval_infinitePi (fun _ â†¦ volume) 0).map_eq]
    _ = alg.p0 := initAlgFunction_map alg

variable [StandardBorelSpace R] [Nonempty R]

lemma hasCondDistrib_altReward_zero (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    HasCondDistrib (altReward alg 0) (altArm alg 0) (stationaryEnv Î½).Î½0 (arrayMeasure Î½) where
  condDistrib_eq := by
    simp only [stationaryEnv_Î½0, (hasLaw_altArm_zero alg Î½).map_eq, altReward_zero]
    sorry

lemma hasCondDistrib_altStep' (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½]
    (n : â„•) :
    HasCondDistrib (altHist alg Â· (n + 1) âŸ¨n + 1, by simpâŸ©) (altHist alg Â· n)
      (Bandit.stepKernel alg Î½ n) (arrayMeasure Î½) where
  condDistrib_eq := by
    simp only [Bandit.stepKernel, stepKernel, stationaryEnv_feedback]
    sorry

lemma hasCondDistrib_altStep (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½]
    (n : â„•) :
    HasCondDistrib (fun Ï‰ â†¦ (altArm alg (n + 1) Ï‰, altReward alg (n + 1) Ï‰))
      (fun Ï‰ (i : Iic n) â†¦ (altArm alg i Ï‰, altReward alg i Ï‰))
      (Bandit.stepKernel alg Î½ n) (arrayMeasure Î½) := by
  convert hasCondDistrib_altStep' alg Î½ n with Ï‰ i
  Â· simp only [altArm]
    rw [altHist_eq _ _ n]
  Â· simp only [altReward]
    rw [altHist_eq _ _ n]

lemma hasCondDistrib_altArm (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] (n : â„•) :
    HasCondDistrib (altArm alg (n + 1)) (fun Ï‰ (i : Iic n) â†¦ (altArm alg i Ï‰, altReward alg i Ï‰))
      (alg.policy n) (arrayMeasure Î½) := by
  convert HasCondDistrib.fst (hasCondDistrib_altStep alg Î½ n)
  simp

lemma hasCondDistrib_altReward (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½]
    (n : â„•) :
    HasCondDistrib (altReward alg (n + 1))
      (fun Ï‰ â†¦ (fun (i : Iic n) â†¦ (altArm alg i Ï‰, altReward alg i Ï‰), altArm alg (n + 1) Ï‰))
      ((stationaryEnv Î½).feedback n) (arrayMeasure Î½) := by
  simp only [stationaryEnv_feedback]
  sorry

lemma isAlgEnvInteraction_arrayMeasure (alg : Algorithm Î± R) (Î½ : Kernel Î± R) [IsMarkovKernel Î½] :
    IsAlgEnvInteraction (altArm alg) (altReward alg) alg (stationaryEnv Î½) (arrayMeasure Î½) where
  hasLaw_action_zero := hasLaw_altArm_zero alg Î½
  hasCondDistrib_reward_zero := hasCondDistrib_altReward_zero alg Î½
  hasCondDistrib_action := hasCondDistrib_altArm alg Î½
  hasCondDistrib_reward := hasCondDistrib_altReward alg Î½

end ArrayModel

end MeasureSpace

end Bandits
